@ABLUP11
SIF ABL:レズっ気 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL_レズっ気
CALL CHECK_JUEL_レズっ気
IF RESULT
	CALL NEED_JUEL_レズっ気
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:レズっ気++
ENDIF

@NEED_JUEL_レズっ気
SELECTCASE ABL:レズっ気
	CASE 0
		ABLUP:GETPALAM("快Ｃ") = 100
		ABLUP:GETPALAM("欲情") = 200
		ABLUPEXP:GETEXP("レズ経験") = 50
	CASE 1
		ABLUP:GETPALAM("快Ｃ") = 500
		ABLUP:GETPALAM("欲情") = 1000
		ABLUPEXP:GETEXP("レズ経験") = 200
	CASE 2
		ABLUP:GETPALAM("快Ｃ") = 1000
		ABLUP:GETPALAM("欲情") = 3000
		ABLUPEXP:GETEXP("レズ経験") = 500
	CASE 3
		ABLUP:GETPALAM("快Ｃ") = 2000
		ABLUP:GETPALAM("欲情") = 8000
		ABLUPEXP:GETEXP("レズ経験") = 1000
	CASE 4
		ABLUP:GETPALAM("快Ｃ") = 5000
		ABLUP:GETPALAM("欲情") = 20000
		ABLUPEXP:GETEXP("レズ経験") = 2000
	CASE 5
		ABLUP:GETPALAM("快Ｃ") = 6000
		ABLUP:GETPALAM("欲情") = 25000
		ABLUPEXP:GETEXP("レズ経験") = 2300
	CASE 6
		ABLUP:GETPALAM("快Ｃ") = 8000
		ABLUP:GETPALAM("欲情") = 30000
		ABLUPEXP:GETEXP("レズ経験") = 2600
	CASE 7
		ABLUP:GETPALAM("快Ｃ") = 10000
		ABLUP:GETPALAM("欲情") = 35000
		ABLUPEXP:GETEXP("レズ経験") = 3000
	CASE 8
		ABLUP:GETPALAM("快Ｃ") = 15000
		ABLUP:GETPALAM("欲情") = 40000
		ABLUPEXP:GETEXP("レズ経験") = 3500
	CASE 9
		ABLUP:GETPALAM("快Ｃ") = 20000
		ABLUP:GETPALAM("欲情") = 45000
		ABLUPEXP:GETEXP("レズ経験") = 4000
	CASE IS >= 10
		LOCAL = ABL:レズっ気*ABL:レズっ気
		ABLUP:GETPALAM("快Ｃ") = 250 * LOCAL
		ABLUP:GETPALAM("欲情") = 500 * LOCAL
		ABLUPEXP:GETEXP("レズ経験") = 50 * LOCAL
ENDSELECT

;恋慕かつMASTERが女
SIF TALENT:恋慕 && SEX(MASTER) == 2
	TIMES ABLUPEXP:GETEXP("レズ経験") , 0.50
;親愛かつMASTERが女
SIF TALENT:親愛 && SEX(MASTER) == 2
	TIMES ABLUPEXP:GETEXP("レズ経験") , 0.30
;淫乱
IF TALENT:淫乱
	TIMES ABLUP:GETPALAM("快Ｃ") , 0.80
	TIMES ABLUP:GETPALAM("欲情") , 0.80
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES ABLUP:GETPALAM("快Ｃ") , 0.50
	TIMES ABLUP:GETPALAM("欲情") , 0.50
ENDIF
;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ｃ") , 2.00
		TIMES ABLUP:GETPALAM("欲情") , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ｃ") , 0.70
		TIMES ABLUP:GETPALAM("欲情") , 0.70
	ENDIF
ENDIF
;女嫌い
IF TALENT:女嫌い
	TIMES ABLUPEXP:GETEXP("レズ経験") , 1.50
	TIMES ABLUP:GETPALAM("欲情") , 1.50
ENDIF
;女好き
IF TALENT:女好き
	TIMES ABLUPEXP:GETEXP("レズ経験") , 0.50
	TIMES ABLUP:GETPALAM("欲情") , 0.50
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀][女好き]なら無視できる）
IF !TALENT:両刀 && !TALENT:女好き
	IF ABL:レズっ気 >= 8
		ABLUPEXP:GETEXP("異常経験") = 5
	ELSEIF ABL:レズっ気 >= 4
		ABLUPEXP:GETEXP("異常経験") = 3
	ELSEIF ABL:レズっ気 >= 3
		ABLUPEXP:GETEXP("異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL_レズっ気
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
	RETURN 0

;快Ｃの珠、欲情の珠、レズ経験が必要
SIF JUEL:快Ｃ >= ABLUP:GETPALAM("快Ｃ") && JUEL:欲情 >= ABLUP:GETPALAM("欲情") && EXP:レズ経験 >= ABLUPEXP:GETEXP("レズ経験")
	RETURN 1

;上記の条件を満たしていないと上げられない
RETURN 0

@ABLUP_MESSAGE_レズっ気
SELECTCASE ABL:レズっ気
	CASE 3, 4, 8
		;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀][女好き]なら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
			SETCOLOR 120, 120, 120
		SIF ABLUPEXP:GETEXP("異常経験")
			PRINTFORM [異常経験{ABLUPEXP:GETEXP("異常経験")}以上]
		RESETCOLOR
ENDSELECT

SIF JUEL:快Ｃ < ABLUP:GETPALAM("快Ｃ")
	SETCOLOR 120, 120, 120
PRINTFORM [快Ｃの珠×{ABLUP:GETPALAM("快Ｃ")}]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:GETPALAM("欲情")
	SETCOLOR 120, 120, 120
PRINTFORM [欲情の珠×{ABLUP:GETPALAM("欲情")}]
RESETCOLOR

SIF EXP:レズ経験 < ABLUPEXP:GETEXP("レズ経験")
	SETCOLOR 120, 120, 120
PRINTFORM [レズ経験{ABLUPEXP:GETEXP("レズ経験")}以上]
RESETCOLOR

