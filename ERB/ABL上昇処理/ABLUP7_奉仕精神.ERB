@ABLUP7
SIF ABL:奉仕精神 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL_奉仕精神
CALL CHECK_JUEL_奉仕精神
IF RESULT
	CALL NEED_JUEL_奉仕精神
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:奉仕精神++
ENDIF

@NEED_JUEL_奉仕精神
SELECTCASE ABL:奉仕精神
	CASE 0
		ABLUP:GETPALAM("恭順") = 10
		ABLUP:GETPALAM("屈服") = 10
		ABLUPEXP:GETEXP("絶頂経験") = 3
		ABLUPEXP:GETEXP("奉仕快楽経験") = 5
	CASE 1
		ABLUP:GETPALAM("恭順") = 100
		ABLUP:GETPALAM("屈服") = 300
		ABLUPEXP:GETEXP("絶頂経験") = 10
		ABLUPEXP:GETEXP("奉仕快楽経験") = 20
	CASE 2
		ABLUP:GETPALAM("恭順") = 500
		ABLUP:GETPALAM("屈服") = 2000
		ABLUPEXP:GETEXP("絶頂経験") = 30
		ABLUPEXP:GETEXP("奉仕快楽経験") = 50
	CASE 3
		ABLUP:GETPALAM("恭順") = 1000
		ABLUP:GETPALAM("屈服") = 5000
		ABLUPEXP:GETEXP("絶頂経験") = 50
		ABLUPEXP:GETEXP("奉仕快楽経験") = 100
	CASE 4
		ABLUP:GETPALAM("恭順") = 3000
		ABLUP:GETPALAM("屈服") = 10000
		ABLUPEXP:GETEXP("絶頂経験") = 100
		ABLUPEXP:GETEXP("奉仕快楽経験") = 150
	CASE 5
		ABLUP:GETPALAM("恭順") = 5000
		ABLUP:GETPALAM("屈服") = 20000
		ABLUPEXP:GETEXP("絶頂経験") = 150
		ABLUPEXP:GETEXP("奉仕快楽経験") = 200
	CASE 6
		ABLUP:GETPALAM("恭順") = 10000
		ABLUP:GETPALAM("屈服") = 30000
		ABLUPEXP:GETEXP("絶頂経験") = 200
		ABLUPEXP:GETEXP("奉仕快楽経験") = 300
	CASE 7
		ABLUP:GETPALAM("恭順") = 15000
		ABLUP:GETPALAM("屈服") = 45000
		ABLUPEXP:GETEXP("絶頂経験") = 250
		ABLUPEXP:GETEXP("奉仕快楽経験") = 400
	CASE 8
		ABLUP:GETPALAM("恭順") = 30000
		ABLUP:GETPALAM("屈服") = 60000
		ABLUPEXP:GETEXP("絶頂経験") = 300
		ABLUPEXP:GETEXP("奉仕快楽経験") = 500
	CASE 9
		ABLUP:GETPALAM("恭順") = 40000
		ABLUP:GETPALAM("屈服") = 80000
		ABLUPEXP:GETEXP("絶頂経験") = 400
		ABLUPEXP:GETEXP("奉仕快楽経験") = 600
	CASE IS >= 10
		LOCAL = ABL:奉仕精神*ABL:奉仕精神
		ABLUP:GETPALAM("恭順") = 500 * LOCAL
		ABLUP:GETPALAM("屈服") = 1000 * LOCAL
		ABLUPEXP:GETEXP("絶頂経験") = 5 * LOCAL
		ABLUPEXP:GETEXP("奉仕快楽経験") = 7 * LOCAL
ENDSELECT

;素質での補正
;恋慕
SIF TALENT:恋慕
	TIMES ABLUP:GETPALAM("恭順") , 0.80
;親愛
SIF TALENT:親愛
	TIMES ABLUP:GETPALAM("恭順") , 0.60
;淫乱
IF TALENT:淫乱
	TIMES ABLUP:GETPALAM("恭順") , 0.90
	TIMES ABLUP:GETPALAM("屈服") , 0.90
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES ABLUP:GETPALAM("恭順") , 0.70
	TIMES ABLUP:GETPALAM("屈服") , 0.70
ENDIF
;服従
SIF TALENT:服従
	TIMES ABLUP:GETPALAM("屈服") , 0.70
;隷属
SIF TALENT:隷属
	TIMES ABLUP:GETPALAM("屈服") , 0.40

;小柄体型、小人体型、幼稚
SIF TALENT:小柄体型 || TALENT:小人体型 || TALENT:幼稚
	TIMES ABLUPEXP:GETEXP("絶頂経験") , 0.75

;悲観的、寂しがり屋
IF TALENT:悲観的 || TALENT:寂しがり屋
	TIMES ABLUP:GETPALAM("恭順") , 0.80
	TIMES ABLUP:GETPALAM("屈服") , 0.80
	TIMES ABLUPEXP:GETEXP("奉仕快楽経験") , 0.80
ENDIF

;陥落、妄信、献身的
IF CFLAG:陥落 || TALENT:妄信 || TALENT:献身的
	TIMES ABLUPEXP:GETEXP("奉仕快楽経験") , 0.50
;反抗的、プライド高い
ELSEIF TALENT:反抗的 || TALENT:プライド高い
	TIMES ABLUPEXP:GETEXP("奉仕快楽経験") , 1.50
ENDIF

;反抗的
SIF TALENT:反抗的
	TIMES ABLUP:GETPALAM("屈服") , 1.20

;臆病
SIF TALENT:臆病
	TIMES ABLUP:GETPALAM("恭順") , 1.20

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETPALAM("恭順") , 2.00
		TIMES ABLUP:GETPALAM("屈服") , 2.00
		TIMES ABLUPEXP:GETEXP("絶頂経験") , 2.00
		TIMES ABLUPEXP:GETEXP("奉仕快楽経験") , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETPALAM("恭順") , 0.50
		TIMES ABLUP:GETPALAM("屈服") , 0.50
		TIMES ABLUPEXP:GETEXP("絶頂経験") , 0.50
		TIMES ABLUPEXP:GETEXP("奉仕快楽経験") , 0.50
	ENDIF
ENDIF

;LV4からは異常経験必要 LV8からは異常経験5以上必要（陥落済みなら無視できる）
IF !CFLAG:陥落
	IF ABL:奉仕精神 >= 8
		ABLUPEXP:GETEXP("異常経験") = 5
	ELSEIF ABL:奉仕精神 >= 4
		ABLUPEXP:GETEXP("異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL_奉仕精神
;従順が奉仕精神レベル以上でないといけない
SIF ABL:従順 < ABL:奉仕精神
	RETURN 0
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
	RETURN 0

;恭順の珠、屈服の珠、絶頂経験、奉仕快楽経験が必要
SIF JUEL:恭順 >= ABLUP:GETPALAM("恭順") && JUEL:屈服 >= ABLUP:GETPALAM("屈服") && EXP:絶頂経験 >= ABLUPEXP:GETEXP("絶頂経験") && EXP:奉仕快楽経験 >= ABLUPEXP:GETEXP("奉仕快楽経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_奉仕精神
SELECTCASE ABL:奉仕精神
	CASE 4, 8
		;LV4からは異常経験必要 LV8からは異常経験5以上必要（陥落済みなら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
			SETCOLOR 120, 120, 120
		SIF ABLUPEXP:GETEXP("異常経験")
			PRINTFORM [異常経験{ABLUPEXP:GETEXP("異常経験")}以上]
		RESETCOLOR
ENDSELECT

SIF ABL:奉仕精神 > ABL:従順
	SETCOLOR 120, 120, 120
SIF ABL:奉仕精神
	PRINTFORM [従順LV{ABL:奉仕精神}以上]
RESETCOLOR

SIF JUEL:恭順 < ABLUP:GETPALAM("恭順")
	SETCOLOR 120, 120, 120
PRINTFORM [恭順の珠×{ABLUP:GETPALAM("恭順")}]
RESETCOLOR

SIF JUEL:屈服 < ABLUP:GETPALAM("屈服")
	SETCOLOR 120, 120, 120
PRINTFORM [屈服の珠×{ABLUP:GETPALAM("屈服")}]
RESETCOLOR

SIF EXP:絶頂経験 < ABLUPEXP:GETEXP("絶頂経験")
	SETCOLOR 120, 120, 120
PRINTFORM [絶頂経験{ABLUPEXP:GETEXP("絶頂経験")}以上]
RESETCOLOR

SIF EXP:奉仕快楽経験 < ABLUPEXP:GETEXP("奉仕快楽経験")
	SETCOLOR 120, 120, 120
PRINTFORM [奉快経験{ABLUPEXP:GETEXP("奉仕快楽経験")}以上]
RESETCOLOR

