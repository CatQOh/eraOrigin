@ABLUP18
SIF ABL:噴乳中毒 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL_噴乳中毒
CALL CHECK_JUEL_噴乳中毒
IF RESULT
	CALL NEED_JUEL_噴乳中毒
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:噴乳中毒++
ENDIF

@NEED_JUEL_噴乳中毒
SELECTCASE ABL:噴乳中毒
	CASE 0
		ABLUP:GETPALAM("快Ｂ") = 6000
		ABLUP:GETPALAM("欲情") = 2500
		ABLUP:GETPALAM("屈服") = 1000
		ABLUPEXP:GETEXP("噴乳経験") = 10
	CASE 1
		ABLUP:GETPALAM("快Ｂ") = 14000
		ABLUP:GETPALAM("欲情") = 5000
		ABLUP:GETPALAM("屈服") = 2500
		ABLUPEXP:GETEXP("噴乳経験") = 25
	CASE 2
		ABLUP:GETPALAM("快Ｂ") = 27000
		ABLUP:GETPALAM("欲情") = 9000
		ABLUP:GETPALAM("屈服") = 4800
		ABLUPEXP:GETEXP("噴乳経験") = 80
	CASE 3
		ABLUP:GETPALAM("快Ｂ") = 65000
		ABLUP:GETPALAM("欲情") = 17000
		ABLUP:GETPALAM("屈服") = 10000
		ABLUPEXP:GETEXP("噴乳経験") = 150
	CASE 4
		ABLUP:GETPALAM("快Ｂ") = 80000
		ABLUP:GETPALAM("欲情") = 30000
		ABLUP:GETPALAM("屈服") = 25000
		ABLUPEXP:GETEXP("噴乳経験") = 250
	CASE 5
		ABLUP:GETPALAM("快Ｂ") = 100000
		ABLUP:GETPALAM("欲情") = 35000
		ABLUP:GETPALAM("屈服") = 30000
		ABLUPEXP:GETEXP("噴乳経験") = 350
	CASE 6
		ABLUP:GETPALAM("快Ｂ") = 120000
		ABLUP:GETPALAM("欲情") = 40000
		ABLUP:GETPALAM("屈服") = 35000
		ABLUPEXP:GETEXP("噴乳経験") = 500
	CASE 7
		ABLUP:GETPALAM("快Ｂ") = 140000
		ABLUP:GETPALAM("欲情") = 45000
		ABLUP:GETPALAM("屈服") = 40000
		ABLUPEXP:GETEXP("噴乳経験") = 600
	CASE 8
		ABLUP:GETPALAM("快Ｂ") = 160000
		ABLUP:GETPALAM("欲情") = 50000
		ABLUP:GETPALAM("屈服") = 45000
		ABLUPEXP:GETEXP("噴乳経験") = 700
	CASE 9
		ABLUP:GETPALAM("快Ｂ") = 200000
		ABLUP:GETPALAM("欲情") = 60000
		ABLUP:GETPALAM("屈服") = 50000
		ABLUPEXP:GETEXP("噴乳経験") = 800
	CASE IS >= 10
		LOCAL = ABL:噴乳中毒*ABL:噴乳中毒
		ABLUP:GETPALAM("快Ｂ") = 2500 * LOCAL
		ABLUP:GETPALAM("欲情") = 700 * LOCAL
		ABLUP:GETPALAM("屈服") = 600 * LOCAL
		ABLUPEXP:GETEXP("噴乳経験") = 9 * LOCAL
ENDSELECT

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ｂ") , 2.00
		TIMES ABLUP:GETPALAM("欲情") , 2.00
		TIMES ABLUP:GETPALAM("屈服") , 2.00
		TIMES ABLUPEXP:GETEXP("噴乳経験") , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ｂ") , 0.80
		TIMES ABLUP:GETPALAM("欲情") , 0.80
		TIMES ABLUP:GETPALAM("屈服") , 0.80
		TIMES ABLUPEXP:GETEXP("噴乳経験") , 0.80
	ENDIF
ENDIF
;自制心
IF TALENT:自制心
	;欲望LV4以下
	IF ABL:欲望 <= 4
		TIMES ABLUP:GETPALAM("快Ｂ") , 2.00
		TIMES ABLUP:GETPALAM("欲情") , 2.00
		TIMES ABLUP:GETPALAM("屈服") , 2.00
		TIMES ABLUPEXP:GETEXP("噴乳経験") , 2.00
	;欲望LV5以上
	ELSEIF ABL:欲望 >= 5
		TIMES ABLUP:GETPALAM("快Ｂ") , 0.80
		TIMES ABLUP:GETPALAM("欲情") , 0.80
		TIMES ABLUP:GETPALAM("屈服") , 0.80
		TIMES ABLUPEXP:GETEXP("噴乳経験") , 0.80
	ENDIF
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES ABLUP:GETPALAM("快Ｂ") , 0.50
	TIMES ABLUP:GETPALAM("欲情") , 0.50
	TIMES ABLUP:GETPALAM("屈服") , 0.50
	TIMES ABLUPEXP:GETEXP("噴乳経験") , 0.50
ENDIF
;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][淫乳]なら無視できる）
IF !TALENT:中毒しやすい && !TALENT:淫乳
	SELECTCASE ABL:噴乳中毒
		CASE IS >= 8
			ABLUPEXP:GETEXP("異常経験") = 7
		CASE IS >= 4
			ABLUPEXP:GETEXP("異常経験") = 3
		CASE IS >= 3
			ABLUPEXP:GETEXP("異常経験") = 2
		CASE IS >= 2
			ABLUPEXP:GETEXP("異常経験") = 1
	ENDSELECT
ENDIF

@CHECK_JUEL_噴乳中毒
;異常経験が必要
SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
	RETURN 0

;B感覚が必要
SIF ABL:Ｂ感覚 <= ABL:噴乳中毒
	RETURN 0

;欲情、快Ｂ、屈服の珠、噴乳経験で上げる
SIF JUEL:快Ｂ >= ABLUP:GETPALAM("快Ｂ") && JUEL:欲情 >= ABLUP:GETPALAM("欲情") && JUEL:屈服 >= ABLUP:GETPALAM("屈服") && EXP:噴乳経験 >= ABLUPEXP:GETEXP("噴乳経験")
	RETURN 1

;上記の条件を満たさないと上げられない
RETURN 0

@ABLUP_MESSAGE_噴乳中毒
SELECTCASE ABL:噴乳中毒
	CASE 2, 3, 4, 8
		;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][淫乳]なら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
			SETCOLOR 120, 120, 120
		SIF ABLUPEXP:GETEXP("異常経験")
			PRINTFORM [異常経験{ABLUPEXP:GETEXP("異常経験")}以上]
		RESETCOLOR
ENDSELECT

SIF ABL:Ｂ感覚 <= ABL:噴乳中毒
	SETCOLOR 120, 120, 120
PRINTFORM [Ｂ感覚LV{ABL:噴乳中毒+1}以上]
RESETCOLOR

SIF JUEL:快Ｂ < ABLUP:GETPALAM("快Ｂ")
	SETCOLOR 120, 120, 120
PRINTFORM [快Ｂの珠×{ABLUP:GETPALAM("快Ｂ")}]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:GETPALAM("欲情")
	SETCOLOR 120, 120, 120
PRINTFORM [欲情の珠×{ABLUP:GETPALAM("欲情")}]
RESETCOLOR

SIF JUEL:屈服 < ABLUP:GETPALAM("屈服")
	SETCOLOR 120, 120, 120
PRINTFORM [屈服の珠×{ABLUP:GETPALAM("屈服")}]
RESETCOLOR

SIF EXP:噴乳経験 < ABLUPEXP:GETEXP("噴乳経験")
	SETCOLOR 120, 120, 120
PRINTFORM [噴乳経験{ABLUPEXP:GETEXP("噴乳経験")}以上]
RESETCOLOR

