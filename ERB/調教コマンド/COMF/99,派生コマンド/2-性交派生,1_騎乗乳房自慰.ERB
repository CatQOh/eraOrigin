;-------------------------------------------------
;騎乗乳房自慰
;-------------------------------------------------
@USERCOM_ABLE_騎乗乳房自慰, ARG
#FUNCTION
;騎乗乳房自慰
;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0
SIF OVER_BUST(TARGET) != ""
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_騎乗乳房自慰
PRINTL 騎乗乳房自慰

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT


;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
SELECTCASE PREVCOMS
	CASE "騎乗位"
		;ABL:Ｖ感覚
		IF ABL:Ｖ感覚
			LOCAL += ABL:Ｖ感覚*2
			PRINTFORM +Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚*2})
		ENDIF
	CASE "騎乗位アナル"
		;ABL:Ａ感覚
		IF ABL:Ａ感覚
			LOCAL += ABL:Ａ感覚*2
			PRINTFORM +Ａ感覚LV{ABL:Ａ感覚}({ABL:Ａ感覚*2})
		ENDIF
ENDSELECT
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

;ABL:性交中毒
IF ABL:性交中毒
	LOCAL += ABL:性交中毒
	PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF
;ABL:露出癖
IF ABL:露出癖
	PRINTFORM +露出癖LV{ABL:露出癖}({ABL:露出癖*4})
	LOCAL += ABL:露出癖*4
ENDIF
;ABL:自慰中毒
IF ABL:自慰中毒
	PRINTFORM +自慰中毒LV{ABL:自慰中毒}({ABL:自慰中毒*3})
	LOCAL += ABL:自慰中毒*3
ENDIF
;快楽刻印
IF MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
	LOCAL += MARK:快楽刻印*3
ENDIF

;PALAM:欲情
LOCAL:1 = GETPALAMLV(PALAM:欲情, 5)
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	TIMES LOCAL:2 , 1.50
IF LOCAL:2
	LOCAL += LOCAL:2*3
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2*3})
ENDIF

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				LOCAL:1 = 20
		CASE "恋慕"
			SIF PLAYER == MASTER
				LOCAL:1 = 15
		CASE "自慰しやすい"
			LOCAL:1 = 5
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -5
		CASE "自制心", "恥じらい"
			LOCAL:1 = -5
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				LOCAL:1 = -12
		CASE "恥薄い"
			LOCAL:1 = 3
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

;媚薬
IF TEQUIP:媚薬
	LOCAL += 8
	PRINT +媚薬(8)
ENDIF

;合計を表示(50以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 50

;難易度上昇
;公開で+10、搾乳器で+5
SIF TEQUIP:ビデオカメラ
	LOCAL:2 += 10
SIF TEQUIP:搾乳器
	LOCAL:2 += 5

SIF LOCAL < LOCAL:2
	PRINT  < 
SIF LOCAL == LOCAL:2
	PRINT  = 
SIF LOCAL > LOCAL:2
	PRINT  > 
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
	CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
SELECTCOMS = 騎乗乳房自慰
SELECTCOM = 505
CALL KOJO_MESSAGE_COM

;派生元を見る
;騎乗位からの派生の場合
IF PREVCOMS == "騎乗位"
	TFLAG:主人挿入箇所 = 1
	;V経験を伴うコマンドのフラグ(処女膜再生対応)
	TFLAG:膣コマンド = 1
	SIF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER && EXP:Ｖ経験 == 0
		TFLAG:恋慕処女 = 1
;騎乗位アナルからの派生の場合
ELSEIF PREVCOMS == "騎乗位アナル"
	TFLAG:主人挿入箇所 = 2
ENDIF

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 800

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-2)*(LOCAL/5)

;PALAM:欲情
SELECTCASE GETPALAMLV(PALAM:欲情, 5)
	CASE 0
		TIMES LOCAL , 0.60
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.20
	CASE 4
		TIMES LOCAL , 1.40
	CASE 5
		TIMES LOCAL , 1.70
ENDSELECT

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;騎乗位からの派生の場合
IF TFLAG:主人挿入箇所 == 1
	;処女だと増える
	SIF TALENT:処女
		TIMES LOCAL , 1.50
ENDIF

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 1200

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/10)*ABL:PLAYER:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;奴隷のABL:Ｖ感覚、Ａ感覚をみる
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		;PALAM:潤滑をみる
		LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-3)*(LOCAL/5)

		SELECTCASE ABL:Ｖ感覚
			CASE 0
				TIMES LOCAL , 0.50
			CASE 1
				TIMES LOCAL , 0.80
			CASE 2
				TIMES LOCAL , 1.00
			CASE 3
				TIMES LOCAL , 1.50
			CASE 4
				TIMES LOCAL , 2.00
			CASEELSE
				LOCAL += (LOCAL/2)*ABL:Ｖ感覚
		ENDSELECT
	CASE 2
		;PALAM:潤滑をみる
		SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
			CASE 0
				TIMES LOCAL , 0.20
			CASE 1
				TIMES LOCAL , 0.50
			CASE 2
				TIMES LOCAL , 1.00
			CASE 3
				TIMES LOCAL , 1.50
			CASE 4
				TIMES LOCAL , 2.50
		ENDSELECT

		SELECTCASE ABL:Ａ感覚
			CASE 0
				TIMES LOCAL , 0.50
			CASE 1
				TIMES LOCAL , 0.80
			CASE 2
				TIMES LOCAL , 1.00
			CASE 3
				TIMES LOCAL , 1.50
			CASE 4
				TIMES LOCAL , 2.00
			CASEELSE
				LOCAL += (LOCAL/2)*ABL:Ａ感覚
		ENDSELECT

		;奴隷が男の場合はホモっ気を見る
		IF SEX(TARGET) == 1
			SELECTCASE ABL:ホモっ気
				CASE 0
					TIMES LOCAL , 0.15
				CASE 1
					TIMES LOCAL , 0.30
				CASE 2
					TIMES LOCAL , 0.50
				CASE 3
					TIMES LOCAL , 0.75
				CASE 4
					TIMES LOCAL , 1.00
				CASEELSE
					LOCAL += (LOCAL/10)*ABL:ホモっ気-3
			ENDSELECT
		;女なら無条件で半減
		ELSE
			TIMES LOCAL , 0.50
		ENDIF
ENDSELECT

;奴隷のABL:Ｂ感覚を見る
SELECTCASE ABL:Ｂ感覚
	CASE 0
		TIMES LOCAL , 0.90
	CASE 1
		TIMES LOCAL , 1.00
	CASE 2
		TIMES LOCAL , 1.10
	CASE 3
		TIMES LOCAL , 1.30
	CASE 4
		TIMES LOCAL , 1.60
	CASEELSE
		LOCAL += (LOCAL/5)*ABL:Ｂ感覚
ENDSELECT

;奴隷のABL:サドっ気をみる
LOCAL += (LOCAL/5)*ABL:サドっ気

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力は分岐後に代入

SOURCE:快B = 300
SOURCE:性行動 = 900
SOURCE:達成 = 800
SOURCE:中毒 = 450
SOURCE:露出 = 300
SOURCE:逸脱 = 400
SOURCE:反感 = 600
SOURCE:不潔 = 400
SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		LOSEBASE:体力 += 100
		LOSEBASE:気力 += 100
		SOURCE:快V = 500
		SOURCE:情愛 = 500
		SOURCE:恭順 = 500
		SOURCE:苦痛 = 500
	;騎乗位アナルからの派生の場合
	CASE 2
		LOSEBASE:体力 += 150
		LOSEBASE:気力 += 150
		SOURCE:快A = 600
		SOURCE:情愛 = 400
		SOURCE:恭順 = 400
		SOURCE:苦痛 = 800
ENDSELECT

DOWNBASE:MASTER:体力 += 60
DOWNBASE:MASTER:気力 += 70

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 900

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) != 1 && TFLAG:主人挿入箇所 == 1 &&  PLAYER != MASTER
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 && TFLAG:主人挿入箇所 == 1
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女 && TFLAG:主人挿入箇所 == 1
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

;騎乗位からの派生の場合
SIF TFLAG:主人挿入箇所 == 1
	CALL 特殊ソース処理_Ｖ

;騎乗位アナルからの派生の場合
SIF TFLAG:主人挿入箇所 == 2
	CALL 特殊ソース処理_Ａ

CALL 特殊ソース処理_セックス

;自慰中毒を見る
SELECTCASE ABL:自慰中毒
	CASE IS <= 1
		TIMES SOURCE:中毒 , 0.00
	CASE 2
		TIMES SOURCE:中毒 , 0.30
	CASE 3
		TIMES SOURCE:中毒 , 0.60
	CASE 4
		TIMES SOURCE:中毒 , 0.90
	CASE 5
		TIMES SOURCE:中毒 , 1.20
	CASE 6
		TIMES SOURCE:中毒 , 1.50
	CASE 7
		TIMES SOURCE:中毒 , 1.80
	CASE 8
		TIMES SOURCE:中毒 , 2.30
	CASE 9
		TIMES SOURCE:中毒 , 3.00
	CASE IS >= 10
		TIMES SOURCE:中毒 , 5.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		LOCAL = 1
	;騎乗位アナルからの派生の場合
	CASE 2
		LOCAL = 2
ENDSELECT

CALL SAMEN_CHECK, LOCAL

SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		LOCAL:1 = 4
	;騎乗位アナルからの派生の場合
	CASE 2
		LOCAL:1 = 5
ENDSELECT

CALL SAMEN_CHECK_T, LOCAL:1

CALL SAMEN_SHOOT, LOCAL, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, LOCAL:1, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		;膣内に破瓜の血の汚れを付加
		SIF TALENT:処女 || TALENT:再生処女
			SETBIT STAIN:膣内, 6
		;奴隷の膣内⇔調教者のペニスの汚れが移動
		STAIN:膣内 |= STAIN:PLAYER:ペニス
		STAIN:PLAYER:ペニス |= STAIN:膣内
	;騎乗位アナルからの派生の場合
	CASE 2
		;奴隷のアナル⇔調教者のペニスの汚れが移動
		SETBIT STAIN:PLAYER:ペニス, 3
		STAIN:アナル |= STAIN:PLAYER:ペニス
		STAIN:PLAYER:ペニス |= STAIN:アナル
ENDSELECT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰経験
;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオカメラ
	EXP:自慰経験 += 2
	PRINTL 自慰経験+2
ELSE
	EXP:自慰経験 += 1
	PRINTL 自慰経験+1
ENDIF
;公開自慰経験
IF TEQUIP:ビデオカメラ && !CFLAG:自慰を撮影された経験
	PRINTL 異常経験+1
	EXP:異常経験++
	CFLAG:自慰を撮影された経験++
ENDIF

SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		;V経験
		PRINTL Ｖ経験+1
		EXP:Ｖ経験++
	;騎乗位アナルからの派生の場合
	CASE 2
		;A経験
		PRINTL Ａ経験+2
		EXP:Ａ経験 += 2
ENDSELECT

;性交経験
PRINTL 性交経験+1
EXP:性交経験++

CALL EXPUP, 5, 3

;嗜虐快楽経験
IF ABL:サドっ気 >= 7
	PRINTFORML 嗜虐快楽経験+{ABL:サドっ気/7}
	EXP:嗜虐快楽経験 += ABL:サドっ気/7
ENDIF

;愛情経験
IF TALENT:恋慕 && PLAYER == MASTER
	PRINTL 愛情経験+2
	EXP:愛情経験 += 2
ELSEIF TALENT:親愛 && PLAYER == MASTER
	PRINTL 愛情経験+4
	EXP:愛情経験 += 4
ENDIF

;依存度補正値(恋慕で2、親愛で4）
IF TALENT:恋慕 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ELSEIF TALENT:親愛 && PLAYER == MASTER
	TFLAG:依存度補正 += 4
ENDIF

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

