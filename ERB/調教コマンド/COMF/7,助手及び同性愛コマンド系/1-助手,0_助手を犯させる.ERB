;-------------------------------------------------
;助手を犯させる
;-------------------------------------------------
@USERCOM_ABLE_助手を犯させる, ARG
#FUNCTION
;助手が居ないとダメ
SIF ASSI == -1
	RETURNF 0
;調教者が主人じゃないとダメ
SIF ASSIPLAY
	RETURNF 0
;助手が小人体型の場合、主人が禁断の知識持ちじゃないとダメ
SIF ASSI > 0 && TALENT:ASSI:小人体型 && !TALENT:MASTER:禁断の知識
	RETURNF 0
;奴隷がペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;オナホール使用中はダメ
SIF TEQUIP:オナホール
	RETURNF 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_助手を犯させる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

CALL COM_ORDER_欲情, 0
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "娼婦"
			LOCAL:1 = 10
		CASE "淫乱"
			LOCAL:1 = 5
		CASE "恥じらい"
			LOCAL:1 = -5
		CASE "献身的"
			LOCAL:1 = 6
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -5
		CASE "快感に素直"
			LOCAL:1 = 3
		CASE "貞操観念"
			LOCAL:1 = -12
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

;助手との相性
SELECTCASE RELATION:(NO:ASSI)
	CASE IS < 100
		LOCAL -= (100-RELATION:(NO:ASSI))/10+1
		PRINTFORM -相性悪い({(100-RELATION:(NO:ASSI))/10+1})
	CASE IS > 100
		LOCAL += (RELATION:(NO:ASSI)-100)/10+1
		PRINTFORM +相性良い({(RELATION:(NO:ASSI)-100)/10+1})
ENDSELECT

;助手が処女
IF TALENT:ASSI:処女
	LOCAL -= 15
	PRINTFORM -助手が処女(15)
ENDIF

;合計を表示(40以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 40
SELECTCASE LOCAL
	CASE IS > LOCAL:2
		PRINT  < 
	CASE IS < LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_助手を犯させる
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回、今回共にの調教者が主人で前回の調教が後背位か後背位アナルのとき
IF TFLAG:前コマンド実行者 == PLAYER
	SELECTCASE PREVCOMS
		CASE "後背位", "後背位アナル"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			TFLAG:数珠繋ぎ = 132
			SELECTCASE PREVCOMS
				CASE "後背位"
					TFLAG:主人挿入箇所 = 1
				CASE "後背位アナル"
					TFLAG:主人挿入箇所 = 2
			ENDSELECT

			IF VAGINA(ASSI)
				TFLAG:奴隷挿入箇所 = 1
			ELSE
				TFLAG:奴隷挿入箇所 = 2
			ENDIF
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
	ENDSELECT
ENDIF

PRINTL 助手を犯させる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

IF VAGINA(ASSI)
	TFLAG:奴隷挿入箇所 = 1
ELSE
	TFLAG:奴隷挿入箇所 = 2
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
TEQUIP:全身上着 = 0
TEQUIP:下半身上着 = 0
TEQUIP:全身上着 = 0

IF !CFLAG:ASSI:挿入可
	TEQUIP:ASSI:下半身上着 = 0
	TEQUIP:ASSI:全身上着 = 0
ENDIF
TEQUIP:ASSI:下半身下着 = 0

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 800

;ABL:助手の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += LOCAL/5*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;ABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.40
	CASE 2
		TIMES LOCAL , 1.80
	CASE 3
		TIMES LOCAL , 2.20
	CASE 4
		TIMES LOCAL , 2.60
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｃ感覚
ENDSELECT

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
LOCAL = 500

;ABL:技巧をみる
LOCAL += LOCAL/10*8*ABL:技巧

IF VAGINA(ASSI)
	;助手のABL:Ｖ感覚をみる
	SELECTCASE ABL:ASSI:Ｖ感覚
		CASE 0
			TIMES LOCAL , 0.50
		CASE 1
			TIMES LOCAL , 0.80
		CASE 2
			TIMES LOCAL , 1.00
		CASE 3
			TIMES LOCAL , 1.50
		CASE 4
			TIMES LOCAL , 2.00
		CASEELSE
			LOCAL += (LOCAL/2)*ABL:Ｖ感覚
	ENDSELECT
ELSE
	;助手のABL:Ａ感覚をみる
	SELECTCASE ABL:ASSI:Ａ感覚
		CASE 0
			TIMES LOCAL , 0.50
		CASE 1
			TIMES LOCAL , 0.80
		CASE 2
			TIMES LOCAL , 1.00
		CASE 3
			TIMES LOCAL , 1.50
		CASE 4
			TIMES LOCAL , 2.00
		CASEELSE
			LOCAL += (LOCAL/2)*ABL:Ａ感覚
	ENDSELECT
ENDIF

CALL 射精ゲージ増加, LOCAL, ASSI

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快C = 500
SOURCE:性行動 = 1000
SOURCE:達成 = 300
SOURCE:屈従 = 500
SOURCE:逸脱 = 500
SOURCE:反感 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;助手が処女
IF TALENT:ASSI:処女
	IF CFLAG:ASSI:陥落
		IF TALENT:ASSI:性転換済
			CFLAG:ASSI:性転換処女の奪われ方 = 2
		ELSE
			CFLAG:ASSI:処女の奪われ方 = 2
		ENDIF
	ELSEIF RELATION:ASSI:(NO:TARGET) > 100
		IF TALENT:ASSI:性転換済
			CFLAG:ASSI:性転換処女の奪われ方 = 3
		ELSE
			CFLAG:ASSI:処女の奪われ方 = 3
		ENDIF		
	ELSE
		IF TALENT:ASSI:性転換済
			CFLAG:ASSI:性転換処女の奪われ方 = 1
		ELSE
			CFLAG:ASSI:処女の奪われ方 = 1
		ENDIF
	ENDIF
	TALENT:ASSI:処女 = 0
	PRINTL ＜助手処女喪失＞
	IF TALENT:ASSI:性転換済
		VIRGIN:ASSI:11 = %NAME:TARGET%
	ELSE
		VIRGIN:ASSI:1 = %NAME:TARGET%
	ENDIF
	;レイプ同然だった場合、屈服逸脱が倍化し、従順が2になる。ただし一度きり
	IF (!CFLAG:ASSI:陥落 && RELATION:ASSI:(NO:TARGET) < 100) && !CFLAG:助手レイプ
		TIMES SOURCE:屈従 , 5.00
		TIMES SOURCE:逸脱 , 3.00
		;異常経験追加（一回きり）
		EXP:異常経験++
		PRINTFORML 異常経験+1(%CALLNAME:TARGET%)
		IF ABL:従順 < 2
			PRINTL そして従順が2になった
			ABL:従順 = 2
		ENDIF
		CFLAG:助手レイプ++
	ENDIF
ENDIF

EXP:ASSI:Ｖ経験 += 1

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 2
CALL SAMEN_CHECK_A, 6

CALL SAMEN_SHOOT_T, 2, NOWEX:射精, ASSI
CALL SAMEN_SHOOT, 6, NOWEX:ASSI:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のペニスにアナル汚れ
SIF !VAGINA(ASSI)
	SETBIT STAIN:ペニス, 3
;奴隷のペニス⇔助手の膣内の汚れが移動
STAIN:ペニス |= STAIN:ASSI:膣内
STAIN:ASSI:膣内 |= STAIN:ペニス

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF SEX(TARGET) == SEX(ASSI)
	CALL EXPUP, 4, 3
ELSE
	CALL EXPUP, 1, 3
ENDIF

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

