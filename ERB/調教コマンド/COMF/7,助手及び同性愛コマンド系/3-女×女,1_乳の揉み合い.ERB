;-------------------------------------------------
;乳の揉み合い
;-------------------------------------------------
@USERCOM_ABLE_乳の揉み合い, ARG
#FUNCTION
;互いに女じゃないとダメ
SIF SEX(TARGET) == 1 || SEX(PLAYER) == 1
	RETURNF 0
;レズっ気3以上限定
SIF ABL:レズっ気 < 3
	RETURNF 0
;上半身下着または上半身上着または全身上着をつけているとダメ
SIF OVER_BUST(TARGET) != ""
	RETURNF 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_乳の揉み合い
PRINTL 乳の揉み合い
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 70

SOURCE:快B = 200
SOURCE:性行動 = 300
SOURCE:恭順 = 200
SOURCE:露出 = 100
SOURCE:不潔 = 50
SOURCE:逸脱 = 100
SOURCE:反感 = 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;調教者が［幼児退行］
IF TALENT:PLAYER:幼児／幼児退行
	TIMES SOURCE:快B , 1.20
	TIMES SOURCE:性行動 , 1.20
ENDIF

;調教者が［幼稚］
IF TALENT:PLAYER:幼稚
	TIMES SOURCE:快B , 1.20
	TIMES SOURCE:性行動 , 1.20
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者の指の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 1

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

