;-------------------------------------------------
;アナルフィスト
;-------------------------------------------------
@USERCOM_ABLE_アナルフィスト, ARG
#FUNCTION
;奴隷が淫乱系陥落か服従系陥落じゃないとダメ
SIF !GROUPMATCH(FALLTYPE(TARGET), 2, 3)
	RETURNF 0
;アナルに何か入ってるとダメ
SIF Ａ使用中(TARGET)
	RETURNF 0
;一定のＡ経験が必要 PLAYERが小柄なら手も小さいので緩和
IF TALENT:PLAYER:小柄体型
	SIF EXP:Ａ経験 < 100
		RETURNF 0
ELSE
	SIF EXP:Ａ経験 < 150
		RETURNF 0
ENDIF
;ASSIの場合、従順3以上かサドじゃないとできない。
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
	RETURNF 0
ENDIF
;小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		RETURNF 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURNF 0

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0

;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_アナルフィスト
PRINTL アナルフィスト
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 100

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES LOCAL , 0.20
	CASE 1
		TIMES LOCAL , 0.50
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.50
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ａ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 300
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 30

SOURCE:快A = 200
SOURCE:苦痛 = 1700
SOURCE:恐怖 = 600
SOURCE:露出 = 100
SOURCE:屈従 = 1500
SOURCE:逸脱 = 1700
SOURCE:反感 = 1700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;フィストなどのAへの大きな苦痛は先に処理しておく
CALL 特殊ソース処理_Ａ強

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル⇔調教者の手の汚れが移動
SETBIT STAIN:PLAYER:手, 3
STAIN:アナル |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:アナル

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
EXP:Ａ経験 += 25
PRINTL Ａ経験+25

;異常経験
IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験 
	EXP:異常経験 += 1
	PRINTL 異常経験+1
ENDIF

EXP:Ａ拡張経験 += 5
PRINTL Ａ拡張経験+5

CALL EXPUP, 3, 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

