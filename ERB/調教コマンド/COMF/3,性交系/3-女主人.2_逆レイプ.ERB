;-------------------------------------------------
;逆レイプ
;-------------------------------------------------
@USERCOM_ABLE_逆レイプ, ARG
#FUNCTION
;調教者が小人体型のとき主人が禁断の知識持ちじゃないとダメ
SIF TALENT:PLAYER:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:小人体型
	RETURNF 0
;調教者が助手の場合は助手が陥落していないとダメ
SIF ASSIPLAY && !CFLAG:ASSI:陥落
	RETURNF 0

;対象がペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;オナホール装備中はダメ
SIF TEQUIP:オナホール
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0
;欲情が一定以上じゃないとダメ
SIF PALAM:欲情 < PALAMLV:3
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_逆レイプ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	VARSET LOCAL, 0
	LOCAL = RESULT

	;ABL:欲望
	IF ABL:欲望
		LOCAL += ABL:欲望*3
		PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
	ENDIF
	;ABL:性交中毒
	IF ABL:性交中毒
		LOCAL += ABL:性交中毒*2
		PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒*2})
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		LOCAL += MARK:快楽刻印*3
		PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
	ENDIF

	CALL COM_ORDER_欲情, 0
	LOCAL += RESULT

	REPEAT VARSIZE("TALENT")
		LOCAL:1 = 0
		SIF !TALENT:COUNT
			CONTINUE
		SELECTCASE TALENTNAME:COUNT
			CASE "恥じらい"
				LOCAL:1 = -5
			CASE "快感の否定"
				SIF FALLTYPE(TARGET) != 2
					LOCAL:1 = -10
			CASE "女嫌い"
				SIF SEX(PLAYER) == 2 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
					LOCAL:1 = -12
		ENDSELECT
		IF LOCAL:1
			PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
			LOCAL += LOCAL:1
		ENDIF
	REND

	;媚薬
	IF TEQUIP:媚薬
		LOCAL += 6
		PRINT +媚薬(6)
	ENDIF

	;合計を表示(32以上で実行)
	PRINTFORM  = {LOCAL}

	LOCAL:2 = 32
	SELECTCASE LOCAL
		CASE IS < LOCAL:2
			PRINT  < 
		CASE IS > LOCAL:2
			PRINT  > 
		CASEELSE
			PRINT  = 
	ENDSELECT
	PRINTFORM 実行値{LOCAL:2}
	WAIT
	;実行できない
	IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
ELSE
	RETURN 1
ENDIF

@COM_逆レイプ
PRINTL 逆レイプ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

IF VAGINA(PLAYER)
	PRINTL どちらで逆レイプしますか？
	PRINTL [1] - 膣で逆レイプする
	PRINTL [2] - アナルで逆レイプする
	DO
		INPUT
		TFLAG:奴隷挿入箇所 = RESULT
	LOOP LOOPRES(1, 2)
ELSE
	TFLAG:奴隷挿入箇所 = 2
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(PLAYER) != ""
	IF CFLAG:PLAYER:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 500

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:奉仕精神をみる
LOCAL += (LOCAL/10)*ABL:奉仕精神

;プレイヤーのABL:Ｖ感覚、Ａ感覚をみる
SELECTCASE TFLAG:奴隷挿入箇所
	CASE 1
		SELECTCASE ABL:PLAYER:Ｖ感覚
			CASE 0
				TIMES LOCAL , 1.00
			CASE 1
				TIMES LOCAL , 1.50
			CASE 2
				TIMES LOCAL , 2.00
			CASE 3
				TIMES LOCAL , 2.50
			CASE 4
				TIMES LOCAL , 3.50
			CASEELSE
				LOCAL += LOCAL*(ABL:PLAYER:Ｖ感覚-1)
		ENDSELECT
	CASE 2
		SELECTCASE ABL:PLAYER:Ａ感覚
			CASE 0
				TIMES LOCAL , 1.00
			CASE 1
				TIMES LOCAL , 1.50
			CASE 2
				TIMES LOCAL , 2.00
			CASE 3
				TIMES LOCAL , 2.50
			CASE 4
				TIMES LOCAL , 3.50
			CASEELSE
				LOCAL += LOCAL*(ABL:PLAYER:Ａ感覚-1)
		ENDSELECT
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 1000

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/2)*ABL:PLAYER:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;ABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 80
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 100
DOWNBASE:MASTER:気力 += 80

SOURCE:快C = 400
SOURCE:恐怖 = 200
SOURCE:性行動 = 300
SOURCE:屈従 = 1000
SOURCE:逸脱 = 500
SOURCE:反感 = 500
SOURCE:露出 = 100
SOURCE:不潔 = 150
SOURCE:恭順 = 400

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;調教者が処女か再生処女
IF TFLAG:奴隷挿入箇所
	IF TALENT:PLAYER:処女 || TALENT:PLAYER:再生処女
		TIMES SOURCE:露出 , 20.00
		TIMES SOURCE:屈従 , 3.00
		PRINTFORML %CALLNAME:PLAYER%処女喪失
		CFLAG:PLAYER:逆レイプで処女を散らした経験++
		IF !CFLAG:処女に逆レイプされた経験
			;異常経験追加
			EXP:異常経験++
			PRINTFORML 異常経験+1
		ENDIF
		CFLAG:処女に逆レイプされた経験++
		SETBIT STAIN:PLAYER:膣内, 6
		WAIT
	ENDIF

	IF TALENT:PLAYER:処女
		IF TALENT:PLAYER:性転換済
			SIF ASSIPLAY
				CFLAG:ASSI:性転換処女の奪われ方 = 2
			VIRGIN:PLAYER:11 = %NAME:TARGET%
		ELSE
			SIF ASSIPLAY
				CFLAG:ASSI:処女の奪われ方 = 2
			VIRGIN:PLAYER:1 = %NAME:TARGET%
		ENDIF
	ENDIF
ENDIF

TALENT:PLAYER:処女 = 0
TALENT:PLAYER:再生処女 = 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精時の処理
SELECTCASE TFLAG:奴隷挿入箇所
	CASE 1
		IF PLAYER == MASTER
			LOCAL = 1
		ELSEIF PLAYER == ASSI
			LOCAL = 2
		ENDIF
	CASE 2
		IF PLAYER == MASTER
			LOCAL = 6
		ELSEIF PLAYER == ASSI
			LOCAL = 7
		ENDIF
ENDSELECT

CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, LOCAL

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, LOCAL, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のペニス⇔調教者への挿入箇所の汚れが移動
SELECTCASE TFLAG:奴隷挿入箇所
	CASE 1
		STAIN:ペニス |= STAIN:PLAYER:膣内
		STAIN:PLAYER:膣内 |= STAIN:ペニス
	CASE 2
		;対象のペニスにアナル汚れ
		SETBIT STAIN:ペニス, 3
		STAIN:ペニス |= STAIN:PLAYER:アナル
		STAIN:PLAYER:アナル |= STAIN:ペニス
ENDSELECT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;調教者のV,A経験
SELECTCASE TFLAG:奴隷挿入箇所
	CASE 1
		PRINTFORML Ｖ経験+1(%CALLNAME:PLAYER%)
		EXP:PLAYER:Ｖ経験++
		PRINTFORML 苦痛快楽経験+1(%CALLNAME:PLAYER%)
		EXP:PLAYER:苦痛快楽経験++
	CASE 2
		PRINTFORML Ａ経験+1(%CALLNAME:PLAYER%)
		EXP:PLAYER:Ａ経験++
		PRINTFORML 苦痛快楽経験+2(%CALLNAME:PLAYER%)
		EXP:PLAYER:苦痛快楽経験 += 2
ENDSELECT

CALL EXPUP, 3, 2

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && !ASSIPLAY
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !ASSIPLAY
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

