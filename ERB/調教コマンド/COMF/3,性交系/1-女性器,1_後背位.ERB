;-------------------------------------------------
;後背位
;-------------------------------------------------
@USERCOM_ABLE_後背位, ARG
#FUNCTION
;調教者がペニス有り、もしくはペニスバンドがないとダメ
SIF PENIS(PLAYER) && !ITEM:ペニスバンド
	RETURNF 0
;膣に何か入ってるとダメ
SIF Ｖ使用中(TARGET)
	RETURNF 0
;ヴァギナ無いとダメ
SIF !VAGINA(TARGET)
	RETURNF 0
;小人体型持ちは、調教者と調教対象が小人体型ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:PLAYER:小人体型
	RETURNF 0
;処女で、従順4以下、レズっ気4以下のASSIの場合正常位はできない（助手がサド、男ならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF SEX(ASSI) == 2 && (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && !TALENT:ASSI:サド
		RETURNF 0
ENDIF
;潤滑不足で、従順3以下のASSIの場合後背位はできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_後背位
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	VARSET LOCAL, 0
	LOCAL = RESULT

	;ABL:欲望
	IF ABL:欲望
		LOCAL += ABL:欲望*3
		PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		LOCAL += ABL:Ｖ感覚*2
		PRINTFORM +Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚*2})
	ENDIF
	;ABL:性交中毒
	IF ABL:性交中毒
		LOCAL += ABL:性交中毒
		PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		LOCAL += MARK:快楽刻印*3
		PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		LOCAL -= 5
		PRINT -潤滑不足(5)
	ENDIF

	CALL COM_ORDER_欲情, 0
	LOCAL += RESULT

	REPEAT VARSIZE("TALENT")
		LOCAL:1 = 0
		SIF !TALENT:COUNT
			CONTINUE
		SELECTCASE TALENTNAME:COUNT
			CASE "親愛"
				SIF PLAYER == MASTER
					LOCAL:1 = 20
			CASE "恋慕"
				SIF PLAYER == MASTER
					LOCAL:1 = 15
			CASE "快感の否定"
				SIF FALLTYPE(TARGET) != 2
					LOCAL:1 = -5
			CASE "恥じらい"
				LOCAL:1 = -5
			CASE "男嫌い"
				SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
					LOCAL:1 = -12
			CASE "処女"
				LOCAL:1 = -40
		ENDSELECT
		IF LOCAL:1
			PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
			LOCAL += LOCAL:1
		ENDIF
	REND

	;V経験
	IF !TALENT:処女 && EXP:Ｖ経験 < EXPLV:2
		LOCAL -= 15
		PRINT -Ｖ経験不足(15)
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		LOCAL += 6
		PRINT +媚薬(6)
	ENDIF
	
	IF CSTR:種族 == "獣"
		LOCAL += 2
		PRINT +獣(2)
	ELSEIF CSTR:種族 == "獣人"
		LOCAL += 1
		PRINT +獣人(1)
	ENDIF

	LOCAL:1 = 0
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 2)
		LOCAL:1 += 5
	;アナルの汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 3)
		LOCAL:1 += 7
	;母乳
	SIF GETBIT(STAIN:PLAYER:ペニス, 4)
		LOCAL:1 += 2
	;尿
	SIF GETBIT(STAIN:PLAYER:ペニス, 7)
		LOCAL:1 += 5

	SIF TALENT:汚れ無視
		LOCAL:1 /= 3
	SIF TALENT:潔癖症
		LOCAL:1 *= 2

	;汚れあり
	IF LOCAL:1
		LOCAL -= LOCAL:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({LOCAL:1})
	ENDIF

	;合計を表示(22以上で実行)
	PRINTFORM  = {LOCAL}

	LOCAL:2 = 22
	SELECTCASE LOCAL
		CASE IS < LOCAL:2
			PRINT  < 
		CASE IS > LOCAL:2
			PRINT  > 
		CASEELSE
			PRINT  = 
	ENDSELECT
	PRINTFORM 実行値{LOCAL:2}
	WAIT
	;実行できない
	IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
ELSE
	RETURN 1
ENDIF

@COM_後背位
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
TFLAG:乱交継続中 = 0
IF PREVCOMS == "3P"
	IF USERCOM_ABLE_3P()
		TFLAG:乱交継続中 = 1
		JUMP COM_3P
	ENDIF
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF TFLAG:前コマンド実行者 != PLAYER
	SELECTCASE PREVCOMS
		;前回の調教が正常位アナルか後背位アナルかフェラチオかイラマチオのとき3Pへ
		CASE "正常位アナル", "後背位アナル", "フェラチオ", "イラマチオ"
			SIF USERCOM_ABLE_3P()
				JUMP COM_3P
		;前回の調教が"正常位させる"か"後背位させる"か"正常位アナルさせる"か"後背位アナルさせる"のとき数珠繋ぎへ
		CASE "正常位させる", "後背位させる", "正常位アナルさせる", "後背位アナルさせる"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			IF TFLAG:前コマンド実行者 == MASTER
				TFLAG:数珠繋ぎ = 231
				SELECTCASE PREVCOMS
					CASE "正常位させる", "後背位させる"
						TFLAG:奴隷挿入箇所 = 1
					CASE "正常位アナルさせる", "後背位アナルさせる"
						TFLAG:奴隷挿入箇所 = 2
				ENDSELECT
				TFLAG:助手挿入箇所 = 1
			ELSEIF TFLAG:前コマンド実行者 == ASSI
				TFLAG:数珠繋ぎ = 132
				SELECTCASE PREVCOMS
					CASE "正常位させる", "後背位させる"
						TFLAG:奴隷挿入箇所 = 1
					CASE "正常位アナルさせる", "後背位アナルさせる"
						TFLAG:奴隷挿入箇所 = 2
				ENDSELECT
				TFLAG:主人挿入箇所 = 1
			ENDIF
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
		;前回の調教が助手を犯すのとき（すなわち、今回の実行者は助手）
		CASE "助手を犯す"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			TFLAG:数珠繋ぎ = 123
			IF VAGINA(ASSI)
				TFLAG:主人挿入箇所 = 1
			ELSE
				TFLAG:主人挿入箇所 = 2
			ENDIF
			TFLAG:助手挿入箇所 = 1
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
	ENDSELECT
;前回、今回共にの調教者が主人で前回の調教が助手を犯させるのとき
ELSEIF TFLAG:前コマンド実行者 == PLAYER && PREVCOMS == "助手を犯させる"
	TFLAG:数珠繋ぎ = 132
	IF VAGINA(ASSI)
		TFLAG:奴隷挿入箇所 = 1
	ELSE
		TFLAG:奴隷挿入箇所 = 2
	ENDIF
	TFLAG:主人挿入箇所 = 1
	IF USERCOM_ABLE_数珠繋ぎ()
		JUMP COM_数珠繋ぎ
	ELSE
		TFLAG:奴隷挿入箇所 = 0
		TFLAG:主人挿入箇所 = 0
	ENDIF
;前のプレイが後背位かつ調教者の技巧3以上の場合、挿入子宮口責めへ派生。
ELSEIF PREVCOMS == "後背位" && ABL:PLAYER:技巧 >= 3
	JUMP COM_挿入子宮口責め
ENDIF

PRINTL 後背位

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER && TALENT:処女
	TFLAG:恋慕処女 = 1

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		VIRGIN:PLAYER:10 = %NAME:TARGET%
	ELSE
		VIRGIN:PLAYER:0 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 1000

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-2)*(LOCAL/5)

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;処女だと増える
SIF TALENT:処女
	TIMES LOCAL , 1.50

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 500

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-3)*(LOCAL/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 80
LOSEBASE:気力 += 140

DOWNBASE:MASTER:体力 += 80
DOWNBASE:MASTER:気力 += 50

SOURCE:快V = 500
SOURCE:情愛 = 250
SOURCE:苦痛 = 400
SOURCE:中毒 = 300
SOURCE:露出 = 150
SOURCE:反感 = 200
SOURCE:恭順 = 300

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*40+60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 1500
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 300
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 1
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT, 1, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
SIF TALENT:処女 || TALENT:再生処女
	SETBIT STAIN:膣内, 6
;奴隷の膣内⇔調教者のペニスの汚れが移動
STAIN:膣内 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験++
PRINTL Ｖ経験+1

;性交経験
EXP:性交経験++
PRINTL 性交経験+1

CALL EXPUP, 3, 2

;愛情経験
IF TALENT:恋慕 && PLAYER == MASTER
	PRINTL 愛情経験+2
	EXP:愛情経験 += 2
ELSEIF TALENT:親愛 && PLAYER == MASTER
	PRINTL 愛情経験+4
	EXP:愛情経験 += 4
ENDIF

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:親愛 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

