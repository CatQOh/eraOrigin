;-------------------------------------------------
;放尿
;-------------------------------------------------
@USERCOM_ABLE_放尿, ARG
#FUNCTION
;コンドーム装備中は出来ない
SIF TEQUIP:コンドーム
	RETURNF 0
;利尿剤使用中かおもらし癖ありでないとダメ
SIF !TEQUIP:利尿剤 && !TALENT:おもらし癖
	RETURNF 0
;従順3以上でないとダメ（お風呂場ならＯＫ）
SIF !TEQUIP:お風呂場 && ABL:従順 < 3
	RETURNF 0
;連続実行はできない
SIF PREVCOMS == "放尿"
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_放尿
PRINTL 放尿
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 200

SOURCE:恐怖 = 200
SOURCE:露出 = 800
SOURCE:屈従 = 500
SOURCE:逸脱 = 700
SOURCE:反感 = 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;場所によって逸脱のソースが上下
;野外
IF TEQUIP:野外プレイ
	TIMES SOURCE:逸脱 , 2.50
	TIMES SOURCE:露出 , 2.50
;鏡の前（羞恥プレイ）
ELSEIF TEQUIP:羞恥プレイ
	TIMES SOURCE:逸脱 , 1.50
	TIMES SOURCE:露出 , 1.50
;風呂場
ELSEIF TEQUIP:お風呂場
	TIMES SOURCE:逸脱 , 0.50
	TIMES SOURCE:露出 , 0.50
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;放尿コマンド時は射精チェックを行わない
;-------------------------------------------------
;CALL SAMEN_CHECK_T, 0

;CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF PENIS(TARGET)
	SETBIT STAIN:ペニス, 7
ELSEIF VAGINA(TARGET)
	SETBIT STAIN:ヴァギナ, 7
ENDIF
SIF TEQUIP:下半身下着
	SETBIT STAIN:下着, 7

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;放尿経験
PRINTFORML 放尿経験+2
EXP:放尿経験 += 2
TEQUIP:利尿剤 -= 2
SIF TEQUIP:利尿剤 <= 0
	TEQUIP:利尿剤 = 0

SIF TEQUIP:ビデオカメラ
	CFLAG:放尿を撮影された経験++
SIF TEQUIP:野外プレイ
	CFLAG:野外放尿経験++
SIF TEQUIP:お風呂場
	CFLAG:風呂場放尿経験++

CALL EXPUP, 1, 3

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

