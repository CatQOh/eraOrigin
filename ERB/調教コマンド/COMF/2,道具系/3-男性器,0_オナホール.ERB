;-------------------------------------------------
;オナホール
;-------------------------------------------------
@USERCOM_ABLE_オナホール, ARG
#FUNCTION
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;性器が露出してないとダメ
SIF !性器露出(TARGET)
	RETURNF 0
;解除はいつでも可能
IF TEQUIP:オナホール
	COMRESULT = 1
	RETURNF 1
ENDIF
;小人体型持ちは、実行者と対象双方が小人体型ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
IF TALENT:小人体型
	SIF !TALENT:MASTER:禁断の知識 && !TALENT:PLAYER:小人体型
		RETURNF 0
ENDIF
;ペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;少なくとも半立ち状態じゃないとダメ
SIF PALAM:勃起 < PALAMLV:2
	RETURNF 0

;Ｐ使用中はダメ
SIF Ｐ使用中(TARGET)
	RETURNF 0

;ASSIの場合、技巧3以上じゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3
		RETURNF 0
ENDIF
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_オナホール
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:触手
	PRINTL 触手搾精
	SELECTCOM = GETNUM(TRAINNAME, "触手搾精")
ELSE
	PRINTL オナホール
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:オナホール, 0

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_オナホール(500), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 80

SOURCE:快C = 300
SOURCE:露出 = 20
SOURCE:不潔 = 50
SOURCE:逸脱 = 30
SOURCE:反感 = 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 30

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手ペニス責めなら、触手体液がペニスに付く
SIF TEQUIP:触手
	SETBIT STAIN:ペニス, 5

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_オナホール
;オナホール装備中
IF TEQUIP:触手
	PRINTL ＜触手ペニス責め中＞
ELSE
	PRINTL ＜オナホール装備中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 20

SOURCE:快C += 50
SOURCE:露出 += 10
SOURCE:不潔 += 10
SOURCE:逸脱 += 15
SOURCE:反感 += 15

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_オナホール(100), TARGET

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;触手経験加算
SIF TEQUIP:触手
	TENTACLE++

RETURN 1

