;-------------------------------------------------
;指挿入れ
;-------------------------------------------------
@USERCOM_ABLE_指挿入れ, ARG
#FUNCTION
;ヴァギナ無いとダメ
SIF !VAGINA(TARGET)
	RETURNF 0
;プレイヤーが助手の場合、助手が女性で従順3以下orレズっ気3以下、対象が潤滑不足の場合指挿入れはできない（サドならＯＫ）
IF PLAYER == ASSI && !TALENT:ASSI:サド
	SIF SEX(ASSI) == 2 && (ABL:ASSI:従順 <= 3 || ABL:ASSI:レズっ気 <= 3) && PALAM:潤滑 < PALAMLV:2
		RETURNF 0
ENDIF

;異物挿入中はダメ
SIF TEQUIP:Ｖ異物
	RETURNF 0
SIF TEQUIP:触手
	RETURNF 0
COMRESULT = 1
RETURNF 1

@COM_指挿入れ
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前コマンドと今回の実行者が同じ
IF PLAYER == TFLAG:前コマンド実行者
	;前コマンド
	SELECTCASE PREVCOMS
		;非処女で前回の調教が指挿入れかつ実行者の技巧3以上だとGスポット刺激に
		CASE "指挿入れ"
			IF ABL:PLAYER:技巧 >= 3 && !TALENT:処女 && !TALENT:再生処女
				SIF USERCOM_ABLE_Gスポット刺激()
					JUMP COM_Gスポット刺激
			ENDIF
		;前回の調教がGスポット刺激なら続行
		CASE "Gスポット刺激"
			SIF USERCOM_ABLE_Gスポット刺激()
				JUMP COM_Gスポット刺激
	ENDSELECT
ENDIF

PRINTL 指挿入れ
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ
SIF !TALENT:処女 && !TALENT:再生処女
	TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 100

;ABL:調教者の技巧をみる
LOCAL += LOCAL*ABL:PLAYER:技巧/5

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES LOCAL , 0.40
	CASE 1
		TIMES LOCAL , 0.60
	CASE 2
		TIMES LOCAL , 0.60
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.20
ENDSELECT

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.50
	CASE 3
		TIMES LOCAL , 4.00
	CASE 4
		TIMES LOCAL , 6.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｖ感覚+2)
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += LOCAL*ABL:マゾっ気/5

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

IF !TALENT:処女 && !TALENT:再生処女
	SOURCE:快V = 100
	SOURCE:苦痛 = 20
	SOURCE:露出 = 10
	SOURCE:不潔 = 30
	SOURCE:反感 = 30
;処女及び再生処女の場合
;V経験が0の時はV経験を伴わない物に派生
;Vの珠は入らないが、快Vが高いと慣らしてあるため処女喪失時に影響
ELSE
	SIF PALAM:快Ｖ < 100
		SOURCE:快V = 60
	SOURCE:苦痛 = 30
	SOURCE:恐怖 = 40
	SOURCE:露出 = 20
	SOURCE:不潔 = 10
	SOURCE:反感 = 50
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
IF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20
	;処女
	SIF TALENT:処女
		SOURCE:鬱屈 += 10
ENDIF

CALL 特殊ソース処理_Ｖ

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者の指の汚れが移動
STAIN:膣内 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
IF !TALENT:処女 && !TALENT:再生処女
	IF TEQUIP:バイブ
		PRINTL Ｖ経験+2
		EXP:Ｖ経験 += 2
	ELSE
		PRINTL Ｖ経験+1
		EXP:Ｖ経験++
	ENDIF
ENDIF

CALL EXPUP, 3, 1

RETURN 1

