;-------------------------------------------------
;吸血させる
;-------------------------------------------------
@USERCOM_ABLE_吸血させる, ARG
#FUNCTION
;その調教中に吸血させてると使えない
SIF TFLAG:吸血済み
	RETURNF 0
;奴隷が陥落済みじゃないとダメ
SIF !CFLAG:陥落
	RETURNF 0
;実行者が主人でない場合は、相性が良くないとダメ
SIF PLAYER != MASTER && RELATION:PLAYER <= 100
	RETURNF 0
;実行者が無機物、もしくは種族:機械だとダメ
SIF TALENT:PLAYER:無機物 || CSTR:PLAYER:種族 == "機械"
	RETURNF 0
;実行者がゾンビだとダメ
SIF TALENT:PLAYER:ゾンビ
	RETURNF 0
;奴隷が吸血鬼じゃないとダメ
SIF !TALENT:吸血鬼
	RETURNF 0
;拘束中はダメ
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_吸血させる
PRINTL 吸血させる
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 0

SOURCE:情愛 = 500
SOURCE:恭順 = 500
SOURCE:逸脱 = 10

;体力回復
PRINTFORML %CALLNAME:TARGET%の体力+500
BASE:体力 += 500

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;愛情経験
IF PLAYER == MASTER
	PRINTL 愛情経験+1
	EXP:愛情経験++
ENDIF
PRINTL 吸血経験+1
EXP:吸血経験++

;吸血済みフラグ
TFLAG:吸血済み = 1

;獣人、獣による催淫
SELECTCASE CSTR:PLAYER:種族
	CASE "獣人", "獣"
		CFLAG:媚薬++
		SIF CFLAG:媚薬%5 == 0
			CFLAG:残数++
ENDSELECT

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕
	TFLAG:依存度補正 += 1
ELSEIF TALENT:親愛
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

