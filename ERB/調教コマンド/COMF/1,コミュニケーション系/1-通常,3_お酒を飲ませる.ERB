;-------------------------------------------------
;お酒を飲ませる
;-------------------------------------------------
@USERCOM_ABLE_お酒を飲ませる, ARG
#FUNCTION
#DIM LCOUNT
;拘束中はダメ
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	RETURNF 0

;妊娠中は飲ませされない
SIF TALENT:妊娠
	RETURNF 0

;機械と無機物は飲めない
SIF TALENT:無機物 || CSTR:種族 == "機械"
	RETURNF 0

COMRESULT = LOCAL
RETURNF LOCAL

@COM_お酒を飲ませる
#DIM DYNAMIC ALC
#DIMS DYNAMIC 製法
#DIM DYNAMIC 材料, 3
#DIM DYNAMIC LCOUNT

VARSET 材料, 0

PRINTL 何を飲ませますか？
PRINTL [1] 缶飲料類
PRINTL [2] 皆伝(日本酒)
PRINTL [3] アルパカ(ワイン)
PRINTL [4] 魔王(焼酎)
PRINTL [5] 倉吉(ウイスキー)
PRINTL [6] スペリオール(ラム)
PRINTL [7] ビーフィーター(ジン)
PRINTL [8] アブソルート(ウォッカ)
PRINTL [9] カルーア
SIF MONTH == 1 && DAYS == 1
	PRINTL [10] お屠蘇(Alc.2%)
;SIF ITEM:スピリタス
;	PRINTL [10] スピリタス
PRINTL [100] やっぱりやめる

DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			PRINTL 何を飲ませますか？
			PRINTL [0] スーパードライ(Alc.5%)
			PRINTL [1] 一番搾り(Alc.5%)
			PRINTL [2] プレミアムモルツ(Alc.5%)
			PRINTL [3] ヱビス(Alc.5%)
			PRINTL [4] ほろよい(Alc.3%)
			PRINTL [5] ストロングゼロ(Alc.9%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0 TO 3
						SELECTCASE RESULT
							CASE 0
								酒名 = スーパードライ
							CASE 1
								酒名 = 一番搾り
							CASE 2
								酒名 = プレミアムモルツ
							CASE 3
								酒名 = ヱビス
						ENDSELECT
						PRINTFORML %酒名%をどのように飲ませますか？
						PRINTL [1] そのまま(Alc.5%)
						PRINTL [2] シャンディガフ(Alc.3%)
						PRINTL [3] パナシェ(Alc.3%)
						PRINTL [4] ビアスプリッツァー(Alc.8%)
						DO
							INPUT
							SELECTCASE RESULT
								CASE 1
									製法 = ビール
									ALC = 5
									BREAK
								CASE 2
									製法 = ビール
									酒名 = シャンディガフ
									ALC = 3
									BREAK
								CASE 3
									製法 = ビール
									酒名 = パナシェ
									ALC = 3
									BREAK
								CASE 4
									製法 = ビール
									酒名 = ビアスプリッツァー
									ALC = 8
									BREAK
							ENDSELECT
						LOOP 1
					CASE 4
						酒名 = ほろよい
						ALC = 3
						BREAK
					CASE 5
						酒名 = ストロングゼロ
						ALC = 9
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 2
			PRINTL 皆伝をどのように飲ませますか？
			PRINTL [1] そのまま(Alc.12%)
			PRINTL [2] 熱燗(Alc.12%)
			PRINTL [3] サムライロック(Alc.3～10%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = 皆伝
						ALC = 12
						BREAK
					CASE 2
						製法 = 熱燗
						酒名 = 皆伝の熱燗
						ALC = 12
					CASE 3
						製法 = ライム割り
						酒名 = サムライロック
						PRINTL アルコール度数を決めてください(3～10%)
						CALL TWO_DIGIT, 10, "%", 3
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 3
			酒名 = アルパカ
			ALC = 12
			BREAK
		CASE 4
			PRINTL 魔王をどのように飲ませますか？
			PRINTL [1] 水割り(Alc.5～20%)
			PRINTL [2] お湯割り(Alc.5～20%)
			PRINTL [3] ロック(Alc.22%)
			PRINTL [4] ストレート(Alc.25%)
			PRINTL [5] 焼酎ハイボール(Alc.5～20%)
			PRINTL [6] 焼酎コーク
			PRINTL [7] 焼酎ジンジャー
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1, 2, 5, 6, 7
						SELECTCASE RESULT
							CASE 1
								製法 = 水割り
								酒名 = 焼酎の水割り
							CASE 2
								製法 = お湯割り
								酒名 = 焼酎のお湯割り
							CASE 5
								製法 = ハイボール
								酒名 = 焼酎ハイボール
							CASE 6
								製法 = コーラ割り
								酒名 = 焼酎コーク
							CASE 7
								製法 = ジンジャー割り
								酒名 = 焼酎ジンジャー
						ENDSELECT
						PRINTL アルコール度数を決めてください(5～20%)
						CALL TWO_DIGIT, 20, "%", 5
						ALC = RESULT
						BREAK
					CASE 3
						酒名 = 焼酎のロック
						ALC = 22
						BREAK
					CASE 4
						酒名 = 焼酎のストレート
						ALC = 25
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 5
			PRINTL 倉吉をどのように飲ませますか？
			PRINTL [1] 水割り(Alc.9～35%)
			PRINTL [2] お湯割り(Alc.9～35%)
			PRINTL [3] ロック(Alc.37%)
			PRINTL [4] ストレート(Alc.40%)
			PRINTL [5] ハイボール(Alc.9～35%)
			PRINTL [6] コークハイ(Alc.9～35%)
			PRINTL [7] ジンジャーハイ(Alc.9～35%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1, 2, 5, 6, 7
						SELECTCASE RESULT
							CASE 1
								製法 = 水割り
								酒名 = ウイスキーの水割り
							CASE 2
								製法 = お湯割り
								酒名 = ウイスキーのお湯割り
							CASE 5
								製法 = ハイボール
								酒名 = ハイボール
							CASE 6
								製法 = コーラ割り
								酒名 = コークハイ
							CASE 7
								製法 = ジンジャー割り
								酒名 = ジンジャーハイ
						ENDSELECT
						PRINTL アルコール度数を決めてください(9～35%)
						CALL TWO_DIGIT, 35, "%", 9
						ALC = RESULT
						BREAK
					CASE 3
						酒名 = 倉吉のロック
						ALC = 37
						BREAK
					CASE 4
						酒名 = 倉吉のストレート
						ALC = 40
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 6
			PRINTL スペリオールをどのように飲ませますか？
			PRINTL [1] ストレート(Alc.40%)
			PRINTL [2] ロック(Alc.37%)
			PRINTL [3] キューバリバー(Alc.9～35%)
			PRINTL [4] ダイキリ(Alc.20～35%)
			PRINTL [5] ラムトニック(Alc.9～35%)
			PRINTL [6] ラムフィズ(Alc.9～35%)
			PRINTL [7] モヒート(Alc.9～35%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = スペリオールのストレート
						ALC = 40
						BREAK
					CASE 2
						酒名 = スペリオールのロック
						ALC = 37
						BREAK
					CASE 3, 4, 5, 6, 7
						SELECTCASE RESULT
							CASE 3
								製法 = コーラ割り
								酒名 = キューバリバー
							CASE 4
								製法 = ライム割り
								酒名 = ダイキリ
							CASE 5
								製法 = トニック割り
								酒名 = ラムトニック
							CASE 6
								製法 = カクテル
								酒名 = ラムフィズ
							CASE 7
								製法 = カクテル
								酒名 = モヒート
						ENDSELECT
						IF 酒名 == "ダイキリ"
							PRINTL アルコール度数を決めてください(20～35%)
							CALL TWO_DIGIT, 35, "%", 20
						ELSE
							PRINTL アルコール度数を決めてください(9～35%)
							CALL TWO_DIGIT, 35, "%", 9
						ENDIF
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 7
			PRINTL ビーフィーターをどのように飲ませますか？
			PRINTL [1] ストレート(Alc.47%)
			PRINTL [2] ロック(Alc.44%)
			PRINTL [3] ジントニック(Alc.9～42%)
			PRINTL [4] ジンリッキー(Alc.9～42%)
			PRINTL [5] ギムレット(Alc.25～42%)
			PRINTL [6] ジンフィズ(Alc.9～42%)
			PRINTL [7] ジンバック(Alc.9～42%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = ビーフィーターのストレート
						ALC = 47
						BREAK
					CASE 2
						酒名 = ビーフィーターのロック
						ALC = 44
						BREAK
					CASE 3, 4, 5, 6, 7
						SELECTCASE RESULT
							CASE 3
								製法 = トニック割り
								酒名 = ジントニック
							CASE 4
								製法 = ハイボール
								酒名 = ジンリッキー
							CASE 5
								製法 = ライム割り
								酒名 = ギムレット
							CASE 6
								製法 = カクテル
								酒名 = ジンフィズ
							CASE 7
								製法 = カクテル
								酒名 = ジンバック
						ENDSELECT
						IF 酒名 == "ギムレット"
							PRINTL アルコール度数を決めてください(25～42%)
							CALL TWO_DIGIT, 42, "%", 25
						ELSE
							PRINTL アルコール度数を決めてください(9～42%)
							CALL TWO_DIGIT, 42, "%", 9
						ENDIF
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 8
			PRINTL アブソルートをどのように飲ませますか？
			PRINTL [1] ストレート(Alc.40%)
			PRINTL [2] ロック(Alc.37%)
			PRINTL [3] ウォッカトニック(Alc.9～35%)
			PRINTL [4] ウォッカリッキー(Alc.9～35%)
			PRINTL [5] ブラックルシアン(Alc.25～37%)
			PRINTL [6] モスコミュール(Alc.9～35%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = アブソルートのストレート
						ALC = 40
						BREAK
					CASE 2
						酒名 = アブソルートのロック
						ALC = 37
						BREAK
					CASE 3, 4, 5, 6
						SELECTCASE RESULT
							CASE 3
								製法 = トニック割り
								酒名 = ウォッカトニック
							CASE 4
								製法 = ハイボール
								酒名 = ウォッカリッキー
							CASE 5
								製法 = カクテル
								酒名 = ブラックルシアン
							CASE 6
								製法 = カクテル
								酒名 = モスコミュール
						ENDSELECT
						IF 酒名 == "ブラックルシアン"
							PRINTL アルコール度数を決めてください(25～37%)
							CALL TWO_DIGIT, 37, "%", 25
						ELSE
							PRINTL アルコール度数を決めてください(9～35%)
							CALL TWO_DIGIT, 35, "%", 9
						ENDIF
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 9
			PRINTL カルーアをどのように飲ませますか？
			PRINTL [1] ストレート(Alc.20%)
			PRINTL [2] ロック(Alc.18%)
			PRINTL [3] カルーアミルク(Alc.5～15%)
			PRINTL [4] カルーアソーダ(Alc.5～15%)
			PRINTL [5] ブラックルシアン(Alc.25～37%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = カルーアのストレート
						ALC = 40
						BREAK
					CASE 2
						酒名 = カルーアのロック
						ALC = 37
						BREAK
					CASE 3, 4, 5
						SELECTCASE RESULT
							CASE 3
								製法 = カクテル
								酒名 = カルーアミルク
							CASE 4
								製法 = ハイボール
								酒名 = カルーアソーダ
							CASE 5
								製法 = カクテル
								酒名 = ブラックルシアン
						ENDSELECT
						IF 酒名 == "ブラックルシアン"
							PRINTL アルコール度数を決めてください(25～37%)
							CALL TWO_DIGIT, 37, "%", 25
						ELSE
							PRINTL アルコール度数を決めてください(5～15%)
							CALL TWO_DIGIT, 15, "%", 5
						ENDIF
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 10
			酒名 = お屠蘇
			ALC = 2
			BREAK
		CASE 100
			PRINTFORMW お酒を飲ませるのを止めました
			RETURN 0
	ENDSELECT
LOOP 1

CALL ALC_ORDER, ALC, 製法

PRINTFORM  = {RESULT}
SELECTCASE RESULT
	CASE IS < 0
		PRINT  < 
	CASE IS > 0
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINT 実効値0
WAIT

TFLAG:アルコール度数 = ALC

;実行できない お酒は特殊の地の文
IF RESULT < 0
	CALL KOJO_MESSAGE_EVENT, "飲酒拒否時"
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

SOURCE:中毒 = 500
SOURCE:露出 = 50
SOURCE:欲情 = 100
SOURCE:恭順 = 100
SOURCE:逸脱 = 300
SOURCE:反感 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
IF TALENT:下戸
	CFLAG:酔 += ALC*2
ELSEIF TALENT:酒豪
	CFLAG:酔 += ALC/2
ELSE
	CFLAG:酔 += ALC
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1

;-------------------------------------------------
;お酒ボーナスの処理
;-------------------------------------------------
@EQUIP_COM_お酒を飲ませる
SOURCE:欲情 += TEQUIP:お酒ボーナス*30
SOURCE:恭順 += TEQUIP:お酒ボーナス*30

;依存度補正がお酒ボーナス最大のメリット
TFLAG:依存度補正 += TEQUIP:お酒ボーナス

;-------------------------------------------------
;お酒を飲ませるコマンドのCOM_ORDERは特殊
;-------------------------------------------------
@ALC_ORDER, ALC, 製法
#DIM DYNAMIC ALC
#DIMS DYNAMIC 製法
#DIM DYNAMIC LCOUNT

LOCAL = 0

IF TALENT:下戸
	PRINTFORM -アルコール度数{ALC}\%({ALC*2})
	LOCAL -= ALC*2
ELSEIF !TALENT:酒豪
	PRINTFORM -アルコール度数{ALC}\%({ALC})
	LOCAL -= ALC
ENDIF

LOCAL:1 = 0

IF TALENT:快感に素直
	LOCAL:1 = CFLAG:酔*3/2
ELSEIF TALENT:快感の否定
	LOCAL:1 = CFLAG:酔/2
ELSE
	LOCAL:1 = CFLAG:酔
ENDIF

IF LOCAL:1 >= 5 && TALENT:下戸
	PRINTFORM +酒が回っている({LOCAL:1/5})
	LOCAL += LOCAL:1/5
ELSEIF LOCAL:1 >= 3
	PRINTFORM +酒が回っている({LOCAL:1/3})
	LOCAL += LOCAL:1/3
ENDIF

IF CFLAG:二日酔い
	PRINT -二日酔い(5)
	LOCAL -= 5
ENDIF

IF CFLAG:陥落
	PRINT +陥落(5)
	LOCAL += 5
ENDIF

IF GETPALAMLV(PALAM:恭順, 10)
	PRINTFORM +恭順({GETPALAMLV(PALAM:恭順, 10)})
	LOCAL += GETPALAMLV(PALAM:恭順, 10)
ENDIF

IF 製法 == "ビール" && !TALENT:酒豪 && (TALENT:小柄体型 || TALENT:下戸)
	PRINT -ビールは苦くて無理(10)
	LOCAL -= 10
	SELECTCASE 酒名
		CASE "シャンディガフ", "パナシェ", "ビアスプリッツァー"
			PRINT +ビールの苦さを軽減(5)
			LOCAL += 5
	ENDSELECT
ENDIF

IF !TALENT:下戸
	SELECTCASE 製法
		CASE "ハイボール", "ビール", "コーラ割り", "ジンジャー割り", "レモン割り", "ライム割り", "トニック割り", "カクテル"
			IF FLAG:気温 >= 200
				PRINTFORM +高気温&冷飲料({(FLAG:気温-190)/10})
				LOCAL += (FLAG:気温-190)/10
			ENDIF
		CASE "お湯割り", "熱燗"
			IF FLAG:気温 <= 100
				PRINTFORM +低気温&温飲料({(110-FLAG:気温)/10})
				LOCAL += (110-FLAG:気温)/10
			ENDIF
	ENDSELECT
ENDIF

SELECTCASE 製法
	CASE "ハイボール"
		PRINT +ハイボールは美味しい(3)
		LOCAL += 3
	CASE "コーラ割り", "ジンジャー割り", "トニック割り", "レモン割り", "ライム割り"
		PRINT +飲みやすいカクテル(5)
		LOCAL += 5
	CASE "カクテル"
		PRINT +美味しいカクテル(7)
		LOCAL += 7
ENDSELECT

IF TALENT:衝動的
	PRINT +衝動的(3)
	LOCAL += 3
ENDIF

IF TALENT:好奇心
	PRINT +好奇心(3)
	LOCAL += 3
ENDIF

IF TALENT:無関心
	PRINT -無関心(2)
	LOCAL -= 2
ENDIF

IF TALENT:自制心
	PRINT -自制心(5)
	LOCAL -= 5
ENDIF

IF TALENT:目立ちたがり
	PRINT +目立ちたがり(3)
	LOCAL += 3
ENDIF

SELECTCASE 製法
	CASE "お湯割り", "熱燗"
		IF TALENT:猫舌
			PRINT -猫舌(3)
			LOCAL -= 3
		ENDIF
ENDSELECT

IF TALENT:小柄体型 && !TALENT:酒豪
	PRINT -子供(3)
	LOCAL -= 3
ENDIF

IF TALENT:MASTER:魅惑
	PRINT +主人が魅惑持ち(2)
	LOCAL += 2
ENDIF

IF ASSI > 0 && TALENT:ASSI:ハイテンション
	PRINT +助手がハイテンション(2)
	LOCAL += 2
ENDIF


RETURN LOCAL

