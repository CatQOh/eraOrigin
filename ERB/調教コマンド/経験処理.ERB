@EXPUP, ARG, ARG:1
IF ARG
	IF SEX(TARGET) == 1 && SEX(PLAYER) == 1
		PRINTFORML ゲイ経験+{ARG}
		EXP:ゲイ経験 += ARG
	ELSEIF SEX(TARGET) == 2 && SEX(PLAYER) == 2
		PRINTFORML レズ経験+{ARG}
		EXP:レズ経験 += ARG
	ENDIF
ENDIF

IF ARG:1
	IF PLAYER == MASTER
		PRINTFORML 主人調教経験+{ARG:1}
		EXP:主人調教経験 += ARG:1
	ELSEIF PLAYER == ASSI
		PRINTFORML 助手調教経験+{ARG:1}
		EXP:助手調教経験 += ARG:1
	ENDIF
ENDIF

RETURN 0

;-------------------------------------------------
;苦痛快楽経験、奉仕快楽経験のチェック
;-------------------------------------------------
@EXP_GOT_CHECK
#DIM ORG
#DIM PAIN
LOCAL = 0

;奉仕系である、奉仕快楽のチェック
ORG = UP:快Ｃ+UP:快Ｖ+UP:快Ａ+UP:快Ｂ

SELECTCASE UP:欲情
	CASE IS >= 10000
		ORG *= 5
	CASE IS >= 5000
		ORG *= 4
	CASE IS >= 3000
		ORG *= 3
	CASE IS >= 500
		ORG *= 2
	CASEELSE
		ORG = 0
ENDSELECT

IF TFLAG:奉仕快楽
	SELECTCASE ORG
		CASE IS >= 12000
			LOCAL = 16
			TIMES UP:恐怖 , 0.65
			TIMES UP:反感 , 0.30
		CASE IS >= 8000
			LOCAL = 12
			TIMES UP:恐怖 , 0.70
			TIMES UP:反感 , 0.40
		CASE IS >= 5000
			LOCAL = 8
			TIMES UP:恐怖 , 0.75
			TIMES UP:反感 , 0.50
		CASE IS >= 3000
			LOCAL = 4
			TIMES UP:恐怖 , 0.80
			TIMES UP:反感 , 0.60
		CASE IS >= 2000
			LOCAL = 2
			TIMES UP:恐怖 , 0.85
			TIMES UP:反感 , 0.70
		CASE IS >= 1000
			LOCAL = 1
			TIMES UP:恐怖 , 0.90
			TIMES UP:反感 , 0.80
	ENDSELECT
	IF LOCAL
		PRINTFORML 奉仕快楽経験+{LOCAL}
		EXP:奉仕快楽経験 += LOCAL
	ENDIF
ENDIF

;苦痛快楽のチェック
LOCAL = 0
IF UP:欲情 >= 3000 && UP:苦痛 >= 2000
	LOCAL = 16
	TIMES UP:恐怖 , 0.65
ELSEIF UP:欲情 >= 2500 && UP:苦痛 >= 1500
	LOCAL = 12
	TIMES UP:恐怖 , 0.70
ELSEIF UP:欲情 >= 1500 && UP:苦痛 >= 1000
	LOCAL = 8
	TIMES UP:恐怖 , 0.75
ELSEIF UP:欲情 >= 1000 && UP:苦痛 >= 500
	LOCAL = 4
	TIMES UP:恐怖 , 0.80
ELSEIF UP:欲情 >= 600 && UP:苦痛 >= 300
	LOCAL = 2
	TIMES UP:恐怖 , 0.85
ELSEIF UP:欲情 >= 300 && UP:苦痛 >= 100
	LOCAL = 1
	TIMES UP:恐怖 , 0.90
ENDIF
IF LOCAL
	PRINTFORML 苦痛快楽経験+{LOCAL}
	EXP:苦痛快楽経験 += LOCAL
	;嗜虐快楽経験
	LOCAL = (ABL:PLAYER:サドっ気+ABL:マゾっ気)/2
	IF LOCAL > 0
		PRINTFORML 嗜虐快楽経験(%CALLNAME:PLAYER%)+{LOCAL}
		EXP:PLAYER:嗜虐快楽経験 += LOCAL
	ENDIF
ENDIF

