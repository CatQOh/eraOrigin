@SHOW_SHOP
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 調教可能
VARSET LOCAL, 0

VARLINE = 0

CALL CVAR_RESET

REPEAT 10000
	SIF EXISTCSV(COUNT) && GETCHARA(COUNT) == -1 && NO:MASTER != COUNT
		ADDCHARA COUNT
REND

;陥落済みなら陥落済みフラグをオン
IF TARGET > 0
	IF FALLTYPE(TARGET) != 0
		CFLAG:陥落 = 1
	ELSE
		CFLAG:陥落 = 0
	ENDIF
ENDIF

;曜日計算
FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

;祝日・記念日の設定
CALL HOLIDAY

;対象か助手が使用不可の場合は外す
SIF TARGET >= 0 && CFLAG:使用不可
	TARGET = -1
SIF ASSI >= 0 && CFLAG:ASSI:使用不可
	ASSI = -1

CALL SHOP_UI

;現助手＋元助手のキャラ数の確認
REPEAT CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(COUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:COUNT:状態 == 2
		助手可能++
REND

;TARGETの他に調教できるキャラがいるかどうか
REPEAT CHARANUM
	;主人は除外
	SIF COUNT == MASTER
		CONTINUE
	;奴隷も除外
	SIF COUNT == TARGET
		CONTINUE
	;使用不可は除外
	SIF CFLAG:COUNT:使用不可
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(COUNT)
		CONTINUE
	調教可能++
REND

VARSET LOCALS, 

;コマンド名は現在全角4文字の範囲で統一
;100番台は行動のコマンド
LOCALS = 　
;TARGETがいれば表示
IF TARGET > 0
	SELECTCASE FALLTYPE(TARGET)
		CASE 1
			LOCALS:1 = %SET_TOOLTIP("[100] - 愛を育む ", @"%CALLNAME:TARGET%と幸せな時間を過ごします")%
		CASE 2
			LOCALS:1 = %SET_TOOLTIP("[100] - 乱れ合う ", @"%CALLNAME:TARGET%と互いの欲望を満たし合います")%
		CASE 3
			LOCALS:1 = %SET_TOOLTIP("[100] - 躾をする ", @"%CALLNAME:TARGET%に権威を示します")%
		CASEELSE
			LOCALS:1 = %SET_TOOLTIP("[100] - 調教開始 ", @"%CALLNAME:TARGET%を調教します")%
	ENDSELECT
ELSE
	LOCALS:1 = %SET_TOOLTIP("[---] - 調教開始 ", "[202] - 対象変更 から調教対象を選択してください", 0, "gray")%
ENDIF
;無条件で表示
LOCALS:2 = %SET_TOOLTIP("[101] - 休憩     ", @"\@ TIME == 0 ? 夜まで休憩します # 翌日まで休憩します \@")%

HTML_PRINT LOCALS+LOCALS:1+LOCALS:2

;200番台は奴隷関連のコマンド
LOCALS = 　
;調教者含め誰かがいれば表示 現在は必ず表示される
LOCALS:1 = %SET_TOOLTIP("[200] - 能力表示 ", @"キャラの様々な情報を表示します")%
;奴隷が一人でもいれば表示
IF CHARANUM > 1
	LOCALS:2 = %SET_TOOLTIP("[201] - 能力上昇 ", @"キャラの能力を上昇させます")%
ELSE
	LOCALS:2 = %SET_TOOLTIP("[---] - 能力上昇 ", "調教可能なキャラが居ません", 0, "gray")%
ENDIF
IF 調教可能
	LOCALS:3 = %SET_TOOLTIP("[202] - 対象変更 ", "お相手を変更します")%
ELSE
	IF TARGET > 0
		LOCALS:3 = %SET_TOOLTIP("[---] - 対象変更 ", @"%CALLNAME:TARGET%以外にキャラが居ません", 0, "gray")%
	ELSE
		LOCALS:3 = %SET_TOOLTIP("[---] - 対象変更 ", "キャラが居ません", 0, "gray")%
	ENDIF
ENDIF
;助手可能キャラが一人でもいれば表示
IF 助手可能
	IF ASSI > 0
		LOCALS:4 = %SET_TOOLTIP("[203] - 助手変更 ", "助手を変更します")%
	ELSE
		LOCALS:4 = %SET_TOOLTIP("[203] - 助手変更 ", "助手を選出します")%
	ENDIF
ELSE
	LOCALS:4 = %SET_TOOLTIP("[---] - 助手変更 ", "助手に選べるキャラは居ません", 0, "gray")%
ENDIF

HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4

;300番台は購入・売却のコマンド
LOCALS = 　
;無条件で表示
LOCALS:1 = %SET_TOOLTIP("[300] - アイテム ", "淫具や素質追加などのアイテムを購入します")%

HTML_PRINT LOCALS+LOCALS:1

;400番台はセーブ・ロード・オプションのシステムコマンド
;セーブ・ロード・オプションは無条件で表示
LOCALS = 　
LOCALS:1 = %SET_TOOLTIP("[400] - SAVE     ", "現在の状況をセーブします")%
LOCALS:2 = %SET_TOOLTIP("[401] - LOAD     ", "セーブデータをロードします。保存していない進行状況は失われます")%
LOCALS:3 = %SET_TOOLTIP("[402] - OPTION   ", "服装やテキスト表示など、様々な設定を変更します")%

HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3

;デバッグモードなら表示
;#;PRINTPLAIN 　
;#;PRINTL [998] - デバッグ

DRAWLINE
PRINTPLAIN 　SHOP　

@USERSHOP
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 調教可能
#DIM DYNAMIC LCOUNT
VARSET LOCAL, 0
;現助手＋元助手のキャラ数の確認
FOR LCOUNT, 0, CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:LCOUNT:状態 == 2
		助手可能++
NEXT

;TARGETの他に調教できるキャラがいるかどうか
FOR LCOUNT, 0, CHARANUM
	;主人は除外
	SIF LCOUNT == MASTER
		CONTINUE
	;奴隷も除外
	SIF LCOUNT == TARGET
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	調教可能++
NEXT

TFLAG:ページ数 = 0

SELECTCASE RESULT
	;調教開始
	CASE 100
		SIF TARGET > 0
			BEGIN TRAIN
	;休憩
	CASE 101
		;休憩フラグを立てる（ここは特別）
		FLAG:休憩フラグ = 2
		BEGIN TURNEND
	;能力表示
	CASE 200
		CALL SHOW_CHARADATA
	;能力上昇
	CASE 201
		SIF CHARANUM > 1
			CALL SHOP_ABLUP
	;奴隷変更
	CASE 202
		SIF 調教可能
			CALL CHANGE_TARGET
	;助手変更
	CASE 203
		SIF 助手可能
			CALL SELECT_ASSI
	;アイテム
	CASE 300
		CALL SHOPITEM_BUY
	;セーブ
	CASE 400
		CALL USERSAVE
	;ロード
	CASE 401
		CALL USERLOAD
	;オプション
	CASE 402
		CALL OPTION
	;デバッグ用
	;#;CASE 998
	;#;	CALL DEBUG
	CASEELSE
		CLEARLINE 1
		REUSELASTLINE {RESULT}に対応するコマンドはありません
ENDSELECT

RETURN 0

@SHOP_UI
;ゲーム内で表示されるショップ画面
;□XXXX年XX月XX日(曜日) X日目(昼夜)
;　天気:XX 気温:XX.X℃ XXX(月齢.XXX) 
;　総奴隷数:X/陥落済み:X
;
;□調教中
;　No.X XXXXXX(♂♀)[ふたなり]【陥落素質】(依存度 ストレス値)
;　従順:LVXX 欲望:LVXX 技巧:LVXX [BURST]
;　Ｃ感:LVXX Ｖ感:LVXX Ａ感:LVXX Ｂ感:LVXX [部位陥落素質]
;　苦痛刻印:LVX [XXX]　快楽刻印:LVX [XXX]　屈服刻印:LVX [XXX]　反発刻印:LVX [XXX]
;　体力[********************************](XXXX/XXXX)[排卵誘発中]
;
;　主人:XXX
;

;日数表示
DRAWLINE
PRINT □
IF YEAR > 0
	PRINTFORM {YEAR}
ELSE
	PRINTFORM BC{ABS(YEAR)+1}
ENDIF
PRINTFORM 年{MONTH}月{DAYS}日(%DOW(FLAG:曜日)%) 

REPEAT 5
	SIF HOLIDAY:COUNT == ""
		BREAK
	PRINTFORM %HOLIDAY:COUNT% 
REND 

PRINTFORM {DAY}日目

IF TIME == 0
	PRINT (昼) 
ELSE
	PRINT (夜) 
ENDIF

PRINTL 

PRINT 　天気:
SELECTCASE FLAG:天気
	CASE 1
		PRINT 晴れ
	CASE 2
		PRINT 曇り
	CASE 3
		PRINT 雨
	CASE 4
		PRINT 大雨
	CASE 5
		PRINT 雷雨
	CASE 6
		PRINT 雪
	CASE 7
		PRINT 大雪
ENDSELECT

PRINTFORM  気温:{FLAG:気温/10}.{ABS(FLAG:気温)%10}℃ 

SELECTCASE 月齢
	CASE 0 TO 199
		PRINT 新月
	CASE 200 TO 299
		PRINT 繊月
	CASE 300 TO 699
		PRINT 三日月
	CASE 700 TO 999
		PRINT 上弦の月
	CASE 1000 TO 1399
		PRINT 十日夜
	CASE 1400 TO 1499
		PRINT 小望月
	CASE 1500 TO 1599
		PRINT 満月
	CASE 1600 TO 1699
		PRINT 十六夜
	CASE 1700 TO 1799 
		PRINT 立待月
	CASE 1800 TO 1899
		PRINT 居待月
	CASE 1900 TO 1999
		PRINT 寝待月
	CASE 2000 TO 2299
		PRINT 二十日月
	CASE 2300 TO 2599
		PRINT 下弦の月
	CASE 2600 TO 2899
		PRINT 暁月
	CASE 2900 TO 3000
		PRINT 三十日月
ENDSELECT

PRINTFORM (月齢:{月齢/100}.\@月齢%100 < 10 ? 0 # \@{月齢%100})
PRINTL 

;奴隷人数表示
LOCAL = 0
;陥落人数計算
REPEAT CHARANUM
	SIF CFLAG:COUNT:陥落
		LOCAL++
REND
PRINTFORML 　総奴隷数:{CHARANUM-1}\/陥落済み:{LOCAL}

;奴隷の状態
IF TARGET > 0
	PRINTL 
	PRINTL □同棲中
	PRINTFORM 　No.{TARGET} %NAME:TARGET%
	IF TALENT:性転換済
		SELECTCASE SEX(TARGET)
			CASE 1
				PRINT (元♀の♂)
			CASE 2
				PRINT (元♂の♀)
		ENDSELECT
	ELSE
		SELECTCASE SEX(TARGET)
			CASE 1
				PRINT (♂)
			CASE 2
				PRINT (♀)
		ENDSELECT
	ENDIF
	SIF TALENT:ふたなり
		PRINT [ふたなり]
	SIF TALENT:妊娠
		PRINT 【妊娠中】
	SIF TALENT:恋慕
		PRINT 【恋慕】
	SIF TALENT:淫乱
		PRINT 【淫乱】
	SIF TALENT:服従
		PRINT 【服従】
	SIF TALENT:親愛
		PRINT 【親愛】
	SIF TALENT:娼婦
		PRINT 【娼婦】
	SIF TALENT:隷属
		PRINT 【隷属】
	SIF TALENT:崩壊
		PRINT 【崩壊】
	PRINTFORM (依存度:{CFLAG:依存度} ストレス値{CFLAG:ストレス値})
	PRINTL 
	
	PRINTFORM 　従順:LV{ABL:従順, 2, LEFT} 欲望:LV{ABL:欲望, 2, LEFT} 技巧:LV{ABL:技巧, 2, LEFT}
	SIF TALENT:リミットバースト
		PRINT 【BURST】
	IF TALENT:淫核
		PRINT 【淫核】
	ELSEIF TALENT:淫膣
		PRINT 【淫膣】
	ELSEIF TALENT:淫尻
		PRINT 【淫尻】
	ELSEIF TALENT:淫乳
		PRINT 【淫乳】
	ENDIF
	PRINTFORML (依存度:{CFLAG:依存度} ストレス値{CFLAG:ストレス値})
	VARSET LOCALS

	LOCALS = %SET_TOOLTIP(@"　従順:LV{ABL:従順, 2, LEFT}", ABL_COMMENT("従順"))%
	LOCALS:1 = %SET_TOOLTIP(@"欲望:LV{ABL:欲望, 2, LEFT}", ABL_COMMENT("欲望"))%
	LOCALS:2 = %SET_TOOLTIP(@"技巧:LV{ABL:技巧, 2, LEFT}", ABL_COMMENT("技巧"))%
	HTML_PRINT LOCALS+LOCALS:1+LOCALS:2
	;PRINTFORM 　従順:LV{ABL:従順, 2, LEFT} 欲望:LV{ABL:欲望, 2, LEFT} 技巧:LV{ABL:技巧, 2, LEFT}

	VARSET LOCALS
	LOCALS = %SET_TOOLTIP(@"　Ｃ感:LV{ABL:Ｃ感覚, 2, LEFT}", ABL_COMMENT("Ｃ感覚"))%
	SIF VAGINA(TARGET)
		LOCALS:1 = %SET_TOOLTIP(@"Ｖ感:LV{ABL:Ｖ感覚, 2, LEFT}", ABL_COMMENT("Ｖ感覚"))%
	LOCALS:2 = %SET_TOOLTIP(@"Ａ感:LV{ABL:Ａ感覚, 2, LEFT}", ABL_COMMENT("Ａ感覚"))%
	LOCALS:3 = %SET_TOOLTIP(@"Ｂ感:LV{ABL:Ｂ感覚, 2, LEFT}", ABL_COMMENT("Ｂ感覚"))%
	HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3
	
	CALL SHOW_INFO_MARK, TARGET
	PRINTFORML 　体力%BARBASE("体力")%
ENDIF
PRINTL 

PRINTFORM 　主人:%NAME:MASTER%
SELECTCASE SEX(MASTER)
	CASE 1
		PRINT (♂)
	CASE 2
		PRINT (♀)
ENDSELECT
SIF TALENT:MASTER:ふたなり
	PRINT [ふたなり]

IF ASSI >= 0
	PRINTFORM 　助手:%NAME:ASSI%
	SELECTCASE SEX(ASSI)
		CASE 1
			PRINT (♂)
		CASE 2
			PRINT (♀)
	ENDSELECT
	SIF TALENT:ASSI:ふたなり
		PRINT [ふたなり]
ENDIF
PRINTL 

DRAWLINE

LOCAL = 0
REPEAT 10000
	SIF !EXISTCSV(COUNT)
		CONTINUE
	IF (CSVCFLAG(COUNT, GETNUM(CFLAG, "誕生日"))/100) == MONTH && (CSVCFLAG(COUNT, 0)%100) == DAYS
		PRINTFORML 　今日は%CSVNAMEF(COUNT)%の誕生日です
		LOCAL++
	ENDIF
REND
IF LOCAL
	PRINTL 　ハッピーバースデー！
	DRAWLINE
ENDIF
LOCAL = 0

