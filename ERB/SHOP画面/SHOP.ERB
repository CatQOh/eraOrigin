@SHOW_SHOP
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 調教可能
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC コマンド表示用, 10
VARSET LOCAL, 0

VARLINE = 0

SIF データID == 0
	データID = GETSECOND()

CALL CVAR_RESET

REPEAT 10000
	SIF EXISTCSV(COUNT) && GETCHARA(COUNT) == -1 && NO:MASTER != COUNT
		ADDCHARA COUNT
REND

;陥落済みなら陥落済みフラグをオン
IF TARGET > 0
	IF FALLTYPE(TARGET) != 0
		CFLAG:陥落 = 1
	ELSE
		CFLAG:陥落 = 0
	ENDIF
ENDIF

;曜日計算
FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

;祝日・記念日の設定
CALL HOLIDAY

;対象か助手が使用不可の場合は外す
SIF TARGET >= 0 && CFLAG:使用不可
	TARGET = -1
SIF ASSI >= 0 && CFLAG:ASSI:使用不可
	ASSI = -1

CALL SHOP_UI

;現助手＋元助手のキャラ数の確認
REPEAT CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(COUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:COUNT:状態 == 2
		助手可能++
REND

;TARGETの他に調教できるキャラがいるかどうか
REPEAT CHARANUM
	;主人は除外
	SIF COUNT == MASTER
		CONTINUE
	;奴隷も除外
	SIF COUNT == TARGET
		CONTINUE
	;使用不可は除外
	SIF CFLAG:COUNT:使用不可
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(COUNT)
		CONTINUE
	調教可能++
REND

VARSET コマンド表示用

;コマンド名は現在全角4文字の範囲で統一
;100番台は行動のコマンド
LOCALS = 　
;TARGETがいれば表示
IF TARGET > 0
	SELECTCASE FALLTYPE(TARGET)
		CASE 1
			コマンド表示用:1 = %SET_TOOLTIP("[100] - 愛を育む ", @"%CALLNAME:TARGET%と幸せな時間を過ごします")%
		CASE 2
			コマンド表示用:1 = %SET_TOOLTIP("[100] - 乱れ合う ", @"%CALLNAME:TARGET%と互いの欲望を満たし合います")%
		CASE 3
			コマンド表示用:1 = %SET_TOOLTIP("[100] - 躾をする ", @"%CALLNAME:TARGET%に権威を示します")%
		CASEELSE
			コマンド表示用:1 = %SET_TOOLTIP("[100] - 調教開始 ", @"%CALLNAME:TARGET%を調教します")%
	ENDSELECT
ELSE
	コマンド表示用:1 = %SET_TOOLTIP("[---] - 調教開始 ", "[202] - 対象変更 から調教対象を選択してください", 0, "gray")%
ENDIF
;無条件で表示
コマンド表示用:2 = %SET_TOOLTIP("[101] - 休憩     ", @"\@ TIME == 0 ? 夜まで休憩します # 翌日まで休憩します \@")%

HTML_PRINT STRJOIN(コマンド表示用, "")

;200番台は奴隷関連のコマンド
VARSET コマンド表示用
コマンド表示用 = 　
;調教者含め誰かがいれば表示 現在は必ず表示される
コマンド表示用:1 = %SET_TOOLTIP("[200] - 能力表示 ", @"キャラの様々な情報を表示します")%
;奴隷が一人でもいれば表示
IF CHARANUM > 1
	コマンド表示用:2 = %SET_TOOLTIP("[201] - 能力上昇 ", @"キャラの能力を上昇させます")%
ELSE
	コマンド表示用:2 = %SET_TOOLTIP("[---] - 能力上昇 ", "調教可能なキャラが居ません", 0, "gray")%
ENDIF
IF 調教可能
	コマンド表示用:3 = %SET_TOOLTIP("[202] - 奴隷変更 ", "調教対象を変更します")%
ELSE
	SIF CHARANUM != 1 && TARGET >= 0
		コマンド表示用:3 = %SET_TOOLTIP("[---] - 奴隷変更 ", @"%CALLNAME:TARGET%以外に調教可能なキャラが居ません", 0, "gray")%
ENDIF
;助手可能キャラが一人でもいれば表示
IF 助手可能
	IF ASSI > 0
		コマンド表示用:4 = %SET_TOOLTIP("[203] - 助手変更 ", "助手を変更します")%
	ELSE
		コマンド表示用:4 = %SET_TOOLTIP("[203] - 助手変更 ", "助手を選出します")%
	ENDIF
ELSE
	コマンド表示用:4 = %SET_TOOLTIP("[---] - 助手変更 ", "助手に選べるキャラは居ません", 0, "gray")%
ENDIF

HTML_PRINT STRJOIN(コマンド表示用, "")

;300番台は購入・売却のコマンド
コマンド表示用 = 　
;無条件で表示
コマンド表示用:1 = %SET_TOOLTIP("[300] - アイテム ", "淫具や素質追加などのアイテムを購入します")%

;変化できるキャラが居れば表示
SIF CMATCH(TALENT:変化, 1)
	コマンド表示用:2 = %SET_TOOLTIP("[301] - 変化     ", "素質[変化]を持った陥落済みのキャラクターを動物の姿や人間の姿に変化させます")%

HTML_PRINT STRJOIN(コマンド表示用, "")

VARSET コマンド表示用
;400番台はセーブ・ロード・オプションのシステムコマンド
;セーブ・ロード・オプションは無条件で表示
LOCALS = 　
LOCALS:1 = %SET_TOOLTIP("[400] - SAVE     ", "現在の状況をセーブします")%
LOCALS:2 = %SET_TOOLTIP("[401] - LOAD     ", "セーブデータをロードします。保存していない進行状況は失われます")%
LOCALS:3 = %SET_TOOLTIP("[402] - OPTION   ", "服装やテキスト表示など、様々な設定を変更します")%

HTML_PRINT STRJOIN(コマンド表示用, "")

;デバッグモードなら表示
;#;PRINTPLAIN 　
;#;PRINTL [998] - デバッグ

DRAWLINE
PRINTPLAIN 　SHOP　

@USERSHOP
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 調教可能
#DIM DYNAMIC LCOUNT
VARSET LOCAL, 0
;現助手＋元助手のキャラ数の確認
FOR LCOUNT, 0, CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:LCOUNT:状態 == 2
		助手可能++
NEXT

;TARGETの他に調教できるキャラがいるかどうか
FOR LCOUNT, 0, CHARANUM
	;主人は除外
	SIF LCOUNT == MASTER
		CONTINUE
	;奴隷も除外
	SIF LCOUNT == TARGET
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	調教可能++
NEXT

TFLAG:ページ数 = 0

SELECTCASE RESULT
	;調教開始
	CASE 100
		SIF TARGET > 0
			BEGIN TRAIN
	;休憩
	CASE 101
		;休憩フラグを立てる（ここは特別）
		FLAG:休憩フラグ = 2
		BEGIN TURNEND
	;能力表示
	CASE 200
		CALL SHOW_CHARADATA
	;能力上昇
	CASE 201
		SIF CHARANUM > 1
			CALL SHOP_ABLUP
	;奴隷変更
	CASE 202
		SIF 調教可能
			CALL CHANGE_TARGET
	;助手変更
	CASE 203
		SIF 助手可能
			CALL SELECT_ASSI
	;アイテム
	CASE 300
		CALL SHOPITEM_BUY
	;変化
	CASE 301
		SIF CMATCH(TALENT:変化, 1)
			CALL 変化
	;セーブ
	CASE 400
		CALL USERSAVE
	;ロード
	CASE 401
		CALL USERLOAD
	;オプション
	CASE 402
		CALL OPTION
	;デバッグ用
	;#;CASE 998
	;#;	CALL DEBUG
	CASEELSE
		CLEARLINE 1
		REUSELASTLINE {RESULT}に対応するコマンドはありません
ENDSELECT

RETURN 0

@SHOP_UI
#DIM DYNAMIC 能力表示用
;ゲーム内で表示されるショップ画面
;□XXXX年XX月XX日(曜日) X日目(昼夜)
;　天気:XX 気温:XX.X℃ XXX(月齢.XXX) 
;　総奴隷数:X/陥落済み:X
;
;□調教中 [750]　前のキャラ [751] 次のキャラ
;　No.X XXXXXX(♂♀)[ふたなり]【陥落素質等】依存度:XX ストレス値:XXX/999
;　従順:LVXX 欲望:LVXX 技巧:LVXX
;　Ｃ感:LVXX Ｖ感:LVXX Ａ感:LVXX Ｂ感:LVXX
;　苦痛刻印:LVX [XXX]　快楽刻印:LVX [XXX]　屈服刻印:LVX [XXX]　反発刻印:LVX [XXX]
;　体力[********************************](XXXX/XXXX)[排卵誘発中]
;
;　主人:XXX
;

;日数表示
CALL CHECKDRAWLINE
PRINT □
CALL 日付表示

SIF FLAG:周回数 > 1
	PRINTFORM  {FLAG:周回数}周目
CALL ゲームモード表示
PRINTL 

PRINTFORML 　天気:%天気()% 気温:%DIV10(FLAG:気温)%℃ %月齢()%(月齢:%DIV100(月齢)%)

;奴隷人数表示
LOCAL = 0
;陥落人数計算
REPEAT CHARANUM
	SIF CFLAG:COUNT:陥落
		LOCAL++
REND
PRINTFORML 　総奴隷数:{CHARANUM-1}\/陥落済み:{LOCAL}

;奴隷の状態
IF TARGET > 0
	PRINTL 
	PRINTPLAIN □同棲中
	IF TARGET > 1
		PRINT [750] 前のキャラ 
	ELSE
		PRINTPLAIN 　　　　　　　　  
	ENDIF
	IF TARGET < CHARANUM-1
		PRINT [751] 次のキャラ
	ELSE
		PRINTPLAIN 　　　　　　　　
	ENDIF
	PRINTFORM 　No.{TARGET} %NAME:TARGET%
	PRINTFORM %性別表示(TARGET)% 
	SIF TALENT:ふたなり
		PRINT [ふたなり]
	SIF TALENT:アニマル
		PRINT [獣]
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF !TALENT:LCOUNT
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT
			CASE "恋慕", "淫乱", "服従", "親愛", "娼婦", "隷属", "崩壊", "淫核", "淫膣", "淫尻", "淫乳"
				PRINTFORM 【%TALENTNAME:LCOUNT%】
			CASE "妊娠"
				PRINT 【妊娠中】
			CASE "リミットバースト"
				PRINT 【BURST】
		ENDSELECT
	NEXT
	PRINTFORM 依存度:{CFLAG:依存度} 
	PRINT ストレス値:
	CALL ストレス表示, TARGET
	PRINTL 
	
	VARSET 能力表示用
	
	能力表示用 = %SET_TOOLTIP(@"　従順:LV{ABL:従順, 2, LEFT}", ABL_COMMENT("従順"))%
	能力表示用:1 = %SET_TOOLTIP(@"欲望:LV{ABL:欲望, 2, LEFT}", ABL_COMMENT("欲望"))%
	能力表示用:2 = %SET_TOOLTIP(@"技巧:LV{ABL:技巧, 2, LEFT}", ABL_COMMENT("技巧"))%
	HTML_PRINT STRJOIN(能力表示用, "")

	VARSET 能力表示用
	能力表示用 = %SET_TOOLTIP(@"　Ｃ感:LV{ABL:Ｃ感覚, 2, LEFT}", ABL_COMMENT("Ｃ感覚"))%
	SIF VAGINA(TARGET)
		能力表示用:1 = %SET_TOOLTIP(@"Ｖ感:LV{ABL:Ｖ感覚, 2, LEFT}", ABL_COMMENT("Ｖ感覚"))%
	能力表示用:2 = %SET_TOOLTIP(@"Ａ感:LV{ABL:Ａ感覚, 2, LEFT}", ABL_COMMENT("Ａ感覚"))%
	能力表示用:3 = %SET_TOOLTIP(@"Ｂ感:LV{ABL:Ｂ感覚, 2, LEFT}", ABL_COMMENT("Ｂ感覚"))%
	HTML_PRINT STRJOIN(能力表示用, "")
	
	CALL SHOW_INFO_MARK, TARGET
	PRINTFORML 　体力%BARBASE("体力")%
	PRINTFORML 　気力%BARBASE("気力")%
ENDIF
PRINTL 

PRINTFORM 　主人:%NAME:MASTER% %性別表示(MASTER)% 
SIF TALENT:MASTER:ふたなり
	PRINT [ふたなり]
PRINTL 

IF ASSI >= 0
	IF TALENT:ASSI:アニマル
		PRINTFORM 　ペット:%NAME:ASSI%
	ELSE
		PRINTFORM 　助手:%NAME:ASSI%
	ENDIF
	PRINTFORM %性別表示(ASSI)% 
	SIF TALENT:ASSI:ふたなり
		PRINT [ふたなり]
	SIF TALENT:ASSI:アニマル
		PRINT [獣]
	PRINTL 
ENDIF

DRAWLINE

LOCAL = 0
REPEAT 10000
	SIF !EXISTCSV(COUNT)
		CONTINUE
	IF (CSVCFLAG(COUNT, GETNUM(CFLAG, "誕生日"))/100) == MONTH && (CSVCFLAG(COUNT, 0)%100) == DAYS
		PRINTFORML 　今日は%CSVNAMEF(COUNT)%の誕生日です
		LOCAL++
	ENDIF
REND
IF LOCAL
	PRINTL 　ハッピーバースデー！
	DRAWLINE
ENDIF
LOCAL = 0

@変化
#DIM DYNAMIC LCOUNT
DRAWLINE
PRINTL □変化させるキャラを選んでください
FOR LCOUNT, 1, CHARANUM
	IF TALENT:LCOUNT:変化
		IF CFLAG:LCOUNT:陥落
			PRINTFORML [{LCOUNT, 2}] %NAME:LCOUNT% 現在：\@ TALENT:LCOUNT:アニマル ? 獣の姿 # 人間の姿 \@
		ELSE
			SETCOLORBYNAME gray
			PRINTFORML [--] %NAME:LCOUNT% 未陥落
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　変化　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASEELSE
			IF CFLAG:RESULT:陥落 && TALENT:RESULT:変化
				LOCAL = RESULT
				IF TALENT:RESULT:アニマル
					;着衣フラグのリセット
					FOR LCOUNT, 40, 47
						CFLAG:LOCAL:LCOUNT = CSVCFLAG(NO:LOCAL, LCOUNT)
					NEXT
					FOR LCOUNT, 100, 107
						CSTR:LOCAL:LCOUNT = %CSVCSTR(NO:LOCAL, LCOUNT)%
					NEXT
					PRINTFORMW %CALLNAME:RESULT%は人間の姿に変化した
					TALENT:RESULT:アニマル = 0
					IF !TRANCE:LOCAL:GETNUM(TALENT, "動物耳")
						TALENT:LOCAL:動物耳 = 0
						TRANCE:LOCAL:GETNUM(TALENT, "動物耳") = 0
					ENDIF
					IF !TRANCE:LOCAL:GETNUM(TALENT, "尻尾")
						TALENT:LOCAL:尻尾 = 0
						TRANCE:LOCAL:GETNUM(TALENT, "尻尾") = 0
					ENDIF
					IF TRANCE:LOCAL:GETNUM(TALENT, "ファーリー")
						TALENT:LOCAL:ファーリー = 0
						TRANCE:LOCAL:GETNUM(TALENT, "ファーリー") = 0
					ENDIF
					TALENT:LOCAL:バストサイズ = TRANCE:LOCAL:GETNUM(TALENT, "バストサイズ")
					TRANCE:LOCAL:GETNUM(TALENT, "バストサイズ") = 0
				ELSE
					PRINTFORMW %CALLNAME:RESULT%は獣の姿に変化した
					;着衣フラグの変更
					FOR LCOUNT, 40, 47
						CFLAG:LOCAL:LCOUNT = 100
					NEXT
					FOR LCOUNT, 100, 107
						CSTR:LOCAL:LCOUNT = 
					NEXT
					TALENT:LOCAL:アニマル = 1
					IF !TALENT:LOCAL:動物耳
						TALENT:LOCAL:動物耳 = 1
						TRANCE:LOCAL:GETNUM(TALENT, "動物耳") = 1
					ENDIF
					IF !TALENT:LOCAL:尻尾
						TALENT:LOCAL:尻尾 = 1
						TRANCE:LOCAL:GETNUM(TALENT, "尻尾") = 1
					ENDIF
					IF !TALENT:LOCAL:ファーリー
						TALENT:LOCAL:ファーリー = 1
						TRANCE:LOCAL:GETNUM(TALENT, "ファーリー") = 1
					ENDIF
					TRANCE:LOCAL:GETNUM(TALENT, "バストサイズ") = TALENT:LOCAL:バストサイズ
					TALENT:LOCAL:バストサイズ = 0
				ENDIF
			ENDIF
			RESTART
	ENDSELECT
LOOP 1
