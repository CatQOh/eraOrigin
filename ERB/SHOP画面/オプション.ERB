;オプション設定処理
@OPTION
DRAWLINE
PRINTL □設定項目を選択してください
PRINTL 　[0] - テキスト設定
PRINTL 　[1] - 着衣関連設定
PRINTL 　[2] - イベント関連の設定
;PRINTL 　[3] - チュートリアル受講
SIF TALENT:MASTER:ドラゴン使い
	PRINTL 　[4] - ドラゴンの名前変更
DRAWLINE
PRINTPLAIN 　OPTION　
PRINTL [100] - 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			CALL TEXT_SETTING
		CASE 1
			CALL CLOTHES_SETTING
		CASE 2
			CALL EVENT_SETTING
;		CASE 3
;			IF FLAG:ゲームモード == 8
;				CALL TEST_TUTORIAL
;			ELSE
;				CALL TUTORIAL
;			ENDIF
;			DRAWLINE
		CASE 4
			IF TALENT:MASTER:ドラゴン使い
				DO
					PRINTL ドラゴンの名前を入力してください(何も入力しない場合、「ドラゴン」になります)
					INPUTS
					SIF RESULTS == ""
						RESULTS = ドラゴン
					PRINTFORML %RESULTS%でよろしいですか？(後からオプション画面で変更できます)
					PRINTL [0] - はい
					PRINTL [1] - いいえ
					INPUT
					SELECTCASE RESULT
						CASE 0
							CSTR:MASTER:ドラゴン = %RESULTS%
							BREAK
						CASE 1
							CONTINUE
					ENDSELECT
				LOOP LOOPRES(0, 1)
			ENDIF
		CASE 100
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 4, 100)

RESTART

;テキスト関連
@TEXT_SETTING
DRAWLINE
PRINTL □テキストの設定をしてください
PRINT 　現在の設定：
IF FLAG:地の文表示 && FLAG:口上表示
	PRINTL 【口上+地の文】
ELSEIF !FLAG:地の文表示 && FLAG:口上表示
	PRINTL 【口上のみ】
ELSEIF FLAG:地の文表示 && !FLAG:口上表示
	PRINTL 【地の文のみ】
ELSEIF !FLAG:地の文表示 && !FLAG:口上表示
	PRINTL 【テキストを表示しない】
ENDIF
PRINTL 　[0] - 口上+地の文
PRINTL 　[1] - 口上のみ
PRINTL 　[2] - 地の文のみ
PRINTL 　[3] - テキストを表示しない
DRAWLINE
PRINTPLAIN 　TEXT　
PRINTL [100] - 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			PRINTL 【口上+地の文】に設定しました
			FLAG:地の文表示 = 1
			FLAG:口上表示 = 1
		CASE 1
			PRINTL 【口上のみ】に設定しました
			FLAG:地の文表示 = 0
			FLAG:口上表示 = 1
		CASE 2
			PRINTL 【地の文のみ】に設定しました
			FLAG:地の文表示 = 1
			FLAG:口上表示 = 0
		CASE 3
			PRINTL 【テキストを表示しない】に設定しました
			FLAG:地の文表示 = 0
			FLAG:口上表示 = 0
		CASE 100
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 100)

RESTART

;初期着衣設定
@CLOTHES_SETTING
DRAWLINE
PRINTL □設定する項目を選択してください
PRINTL 　[0] - 調教時の着衣状態設定
PRINTL 　[1] - 服装の設定
DRAWLINE
PRINTPLAIN 　CLOTHES　
PRINTL [100] - 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			CALL CLOTHES_SETTING_TRAIN
		CASE 1
			CALL CLOTHES_SETTING_USUALLY
		CASE 100
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 100)

RESTART

;調教開始時の着衣設定
@CLOTHES_SETTING_TRAIN
DRAWLINE
PRINTL □調教開始時の服装を設定してください
PRINT 　現在の設定：
SELECTCASE FLAG:着衣設定
	CASE 0
		PRINTL 全着
	CASE 1
		PRINTL 下着無し
	CASE 2
		PRINTL 下着姿
	CASE 3
		PRINTL 全裸
ENDSELECT
PRINTL 　[0] - 全着
PRINTL 　[1] - 下着無し
PRINTL 　[2] - 下着姿
PRINTL 　[3] - 全裸
DRAWLINE
PRINTPLAIN 　CLOTHES SETTING　
PRINTL [100] - 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			PRINTW 初期の着衣を全着に設定しました
			FLAG:着衣設定 = 0
		CASE 1
			PRINTW 初期の着衣を下着無しに設定しました
			FLAG:着衣設定 = 1
		CASE 2
			PRINTW 初期の着衣を下着姿に設定しました
			FLAG:着衣設定 = 2
		CASE 3
			PRINTW 初期の着衣を全裸に設定しました
			FLAG:着衣設定 = 3
		CASE 100
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 100)

RESTART

;着衣変更 きせかえ
@CLOTHES_SETTING_USUALLY
DRAWLINE
PRINTL □着衣の設定をするキャラを選択してください
FOR PAGING, 0, CHARANUM
	CALL PAGING, 1
NEXT

SIF RESULT == -1
	RETURN 0

LOCAL = TARGET
TARGET = RESULT
CALL CHARA_CLOTHES_SETTING
TARGET = LOCAL

RESTART

@CHARA_CLOTHES_SETTING
#DIM LCOUNT
DRAWLINE
PRINTFORML □%NAME:TARGET%の服装
CALL SHOW_INFO_CLOTHES, TARGET
DRAWLINE
PRINTL □着衣の変更をしたい部位を選択してください
;特殊
IF CFLAG:特殊 != 100
	PRINTL 　[0] 特殊
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 特殊
	PRINTL 
	RESETCOLOR
ENDIF
;上半身上着
IF CFLAG:上半身上着 != 100
	PRINTL 　[1] 上半身上着
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 上半身上着
	PRINTL 
	RESETCOLOR
ENDIF
;下半身上着
IF CFLAG:下半身上着 != 100
	PRINTL 　[2] 下半身上着
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 下半身上着
	PRINTL 
	RESETCOLOR
ENDIF
;全身上着
IF CFLAG:全身上着 != 100
	PRINTL 　[3] 全身上着
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 全身上着
	PRINTL 
	RESETCOLOR
ENDIF
;上半身下着
IF CFLAG:上半身下着 != 100
	PRINTL 　[4] 上半身下着
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 上半身下着
	PRINTL 
	RESETCOLOR
ENDIF
;下半身下着
IF CFLAG:下半身下着 != 100
	PRINTL 　[5] 下半身下着
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 下半身下着
	PRINTL 
	RESETCOLOR
ENDIF
;足元
IF CFLAG:足元 != 100
	PRINTL 　[6] 足元
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 足元
	PRINTL 
	RESETCOLOR
ENDIF

;下半身上着もしくは全身上着を着用時、挿入可フラグの変更ができる
IF CSTR:下半身上着 != "" || CSTR:全身上着 != ""
	PRINTFORML 　[7] 着衣状態での性行為可否の変更(現在:\@ CFLAG:挿入可 ? 可能 # 不可能 \@)
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 着衣状態での性行為可否の変更
	PRINTL 
	RESETCOLOR
ENDIF

;全身上着の着用時、全身上着の性質の変更ができる
IF CSTR:全身上着 != ""
	PRINTFORM 　[8] %CSTR:全身上着%の性質の変更(現在:
	SELECTCASE CFLAG:着衣併用
		CASE 0
			PRINTL 通常の全身上着)
		CASE 1
			PRINTL 上着との併用が可能)
		CASE 2
			PRINTL 下着との併用が不可能)
	ENDSELECT
ELSE
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] 全身上着の性質の変更
	PRINTL 
	RESETCOLOR
ENDIF

PRINTL 　[9] 着衣設定の初期化
DRAWLINE
PRINTPLAIN 　COORDINATE　
PRINTL [10] 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 6
			CALL CHARA_CLOTHES_SETTINGNEXT, RESULT
			BREAK
		CASE 7
			INVERTBIT CFLAG:挿入可, 0
			RESTART
		CASE 8
			PRINTFORML □%CSTR:全身上着%の性質を選択してください
			PRINTL 　[0] - 通常の全身上着(上半身上着、下半身上着との併用が不可)
			PRINTL 　[1] - 上着の上に着用するもの(上半身上着、下半身上着との併用が可能)
			PRINTL 　[2] - 肌着も兼ねるもの(上半身上着、下半身上着、及び下着との併用が不可能)
			DO
				INPUT
			LOOP LOOPRES(0, 1, 2)
			CFLAG:着衣併用 = RESULT
			RESTART
		CASE 9
			;対象が「あなた」
			IF NO:TARGET == 0
				;「あなた」が男の場合
				SELECTCASE SEX(TARGET)
					CASE 1
						FOR COUNT, 100, 107
							CSTR:COUNT = 
						NEXT
						CSTR:MASTER:上半身上着 = Tシャツ
						CSTR:MASTER:下半身上着 = ズボン
						CSTR:MASTER:下半身下着 = トランクス
						CFLAG:MASTER:下着パイズリ = 4
					;「あなた」が女の場合
					CASE 2
						FOR COUNT, 100, 107
							CSTR:COUNT = 
						NEXT
						CSTR:MASTER:全身上着 = ワンピース
						CSTR:MASTER:上半身下着 = ブラジャー
						CSTR:MASTER:下半身下着 = パンティ
						CFLAG:MASTER:挿入可 = 1
						CFLAG:MASTER:下着パイズリ = 3
				ENDSELECT
			ELSE
				FOR LCOUNT, 100, 107
					CSTR:LCOUNT = %CSVCSTR(NO:TARGET, LCOUNT, 0)%
				NEXT
				CFLAG:挿入可 = CSVCFLAG(NO:TARGET, GETNUM(CFLAG, "挿入可"))
				CFLAG:着衣併用 = CSVCFLAG(NO:TARGET, GETNUM(CFLAG, "着衣併用"))
				CFLAG:上着パイズリ = CSVCFLAG(NO:TARGET, GETNUM(CFLAG, "上着パイズリ"))
				CFLAG:下着パイズリ = CSVCFLAG(NO:TARGET, GETNUM(CFLAG, "下着パイズリ"))
			ENDIF
			PRINTFORMW ＜%NAME:TARGET%の服装を初期化しました＞
			RESTART
		CASE 10
			RETURN 0
	ENDSELECT
LOOP 1

RESTART

@CHARA_CLOTHES_SETTINGNEXT, ARG
#DIM DYNAMIC LCOUNT
DRAWLINE
PRINTL □何を着せますか？(番号の他、自由入力も可能です)
PRINTL 　[0] 何も着せない
FOR LCOUNT, (ARG*100), (ARG*100+100)
	SIF STR:LCOUNT == ""
		CONTINUE
	PRINTFORML 　[{LCOUNT}] %STR:LCOUNT%
NEXT
DRAWLINE
PRINTPLAIN 　COORDINATE　
PRINTL [1000] 戻る
DO
	INPUTS
	
	IF ISNUMERIC(RESULTS)
		LOCAL = TOINT(RESULTS)
	ELSE
		LOCAL = -1
	ENDIF
	
	SELECTCASE LOCAL
		CASE 0 TO 999
			CSTR:(ARG+100) = %STR:LOCAL%
			SELECTCASE CSTR:全身上着
				CASE "コート", "エプロン", "ローブ"
					CFLAG:着衣併用 = 1
				CASE "バニースーツ", "スクール水着"
					CFLAG:着衣併用 = 2
				CASEELSE
					CFLAG:着衣併用 = 0
			ENDSELECT
			SELECTCASE CSTR:下半身上着
				CASE "スカート", "パレオ"
					CFLAG:挿入可 = 1
				CASEELSE
					CFLAG:挿入可 = 0
			ENDSELECT
			SELECTCASE CSTR:全身上着
				CASE "和服", "ローブ", "ワンピース", "チュチュ", "ドレス", "チャイナドレス", "修道服", "ゴシックロリータ", "メイド服"
					CFLAG:挿入可 = 1
				CASEELSE
					CFLAG:挿入可 = 0
			ENDSELECT
			SELECTCASE ARG
				CASE 1, 2
					SIF CFLAG:着衣併用 != 1
						CSTR:全身上着 = 
				CASE 4, 5
					SIF CFLAG:着衣併用 == 2
						CSTR:全身上着 = 
				CASE 3
					IF CFLAG:着衣併用 != 1
						CSTR:上半身上着 = 
						CSTR:下半身上着 = 
					ENDIF
					IF CFLAG:着衣併用 == 2
						CSTR:上半身下着 = 
						CSTR:下半身下着 = 
					ENDIF
			ENDSELECT
			BREAK
		CASE -1
			CSTR:(ARG+100) = %RESULTS%
			SELECTCASE ARG
				CASE 1, 2
					SIF CFLAG:着衣併用 != 1
						CSTR:全身上着 = 
				CASE 4, 5
					SIF CFLAG:着衣併用 == 2
						CSTR:全身上着 = 
				CASE 3
					IF CFLAG:着衣併用 != 1
						CSTR:上半身上着 = 
						CSTR:下半身上着 = 
					ENDIF
					IF CFLAG:着衣併用 == 2
						CSTR:上半身下着 = 
						CSTR:下半身下着 = 
					ENDIF
			ENDSELECT
			BREAK
		CASE 1000
			RETURN 0
	ENDSELECT
LOOP LOOPRES(1000)

;イベント関連の設定
@EVENT_SETTING
DRAWLINE
PRINTL □イベントのオン/オフを設定できます
PRINTFORML 　[0] - 主人による色仕掛け/夜這い(現在\@ FLAG:色仕掛け ? オン # オフ \@)
DRAWLINE
PRINTPLAIN 　EVENT　
PRINTL [100] - 戻る

INPUT
DO
	SELECTCASE RESULT
		CASE 0
			INVERTBIT FLAG:色仕掛け, 0
			RESTART
		CASE 100
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 100)

;デフォルトオプションの設定
@DEFAULT_OPTION
DRAWLINE
LOADGLOBAL
PRINTL □初期設定のオプションを設定することにより、ニューゲーム時のオプション設定を省略することができます
;PRINTFORML 　[0] - チュートリアル表示(現在:\@ チュートリアル設定 == 1 ? 常時確認 # 省略 \@)
PRINT 　[1] - 口上&地の文設定(現在:
IF 地の文設定 && 口上設定
	PRINTL 口上+地の文)
ELSEIF !地の文設定 && 口上設定
	PRINTL 口上のみ)
ELSEIF 地の文設定 && !口上設定
	PRINTL 地の文のみ)
ELSEIF !地の文設定 && !口上設定
	PRINTL テキストを表示しない)
ENDIF
PRINTFORML 　[2] - 調教開始時の着衣設定(現在:\@ 着衣オプション設定 ? 着せたまま始める # 脱がせて始める \@)
DRAWLINE
PRINTPLAIN 　DEFAULT OPTION　
PRINT [100] - 保存して戻る 
PRINTL [101] - 保存せずに戻る
INPUT
DO
	SELECTCASE RESULT
		CASE 100
			デフォルトオプション = 1
			SAVEGLOBAL
			RETURN 0
		CASE 101
			LOADGLOBAL
			RETURN 0
		;CASE 0
		;	PRINTL □常にチュートリアルの表示を確認しますか？
		;	PRINTL 　[0] - 確認しなくてもいいよ
		;	PRINTL 　[1] - 不安だから確認して
		;	INPUT
		;	DO
		;	LOOP LOOPRES(0, 1)
		;	チュートリアル設定 = RESULT
		;	RESTART
		CASE 1
			PRINTL □口上と地の文の初期設定を選んでください
			PRINTL 　[0] - 口上+地の文
			PRINTL 　[1] - 口上のみ
			PRINTL 　[2] - 地の文のみ
			PRINTL 　[3] - テキストを表示しない
			INPUT
			DO
			LOOP LOOPRES(0, 1, 2, 3)
			SELECTCASE RESULT
				CASE 0
					地の文設定 = 1
					口上設定 = 1
				CASE 1
					地の文設定 = 0
					口上設定 = 1
				CASE 2
					地の文設定 = 1
					口上設定 = 0
				CASE 3
					地の文設定 = 0
					口上設定 = 0
			ENDSELECT
			RESTART
		CASE 2
			PRINTL □調教開始時の着衣設定を選んでください
			PRINTL 　[0] - 着衣プレイはエロいから着せたまま始める
			PRINTL 　[1] - さっさとヤりたいので脱がせてから始める
			INPUT
			DO
			LOOP LOOPRES(0, 1)
			着衣オプション設定 = RESULT
			RESTART
	ENDSELECT
LOOP 1

