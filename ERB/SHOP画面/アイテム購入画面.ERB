@SHOPITEM_BUY
#DIM DYNAMIC 表示数
#DIM DYNAMIC LCOUNT
表示数 = 0
VARSET LOCAL, 0
VARSET ITEMSALES, 0

;素質付与アイテム
;汚れ無視
SIF !TALENT:MASTER:汚れ無視
	ITEMSALES:【汚れ無視】 = 1

;ドラキュラの牙
SIF !TALENT:MASTER:吸血鬼
	ITEMSALES:【ドラキュラの牙】 = 1

;ドラゴンの成体
SIF !TALENT:MASTER:ドラゴン使い
	ITEMSALES:【ドラゴンの成体】 = 1

ITEMSALES:怪しげな栄養剤 = 1
ITEMSALES:魔剤 = 1

;素質変化の薬
ITEMSALES:ふたなり薬 = 1
ITEMSALES:ふたなり消去薬 = 1
ITEMSALES:天使のミルク = 1
ITEMSALES:ユニコーンテイル = 1
ITEMSALES:豊胸剤 = 1
ITEMSALES:縮胸剤 = 1
ITEMSALES:止乳剤 = 1
ITEMSALES:夜尿症改善薬 = 1
ITEMSALES:ゾンビパウダー = 1
ITEMSALES:聖水 = 1
ITEMSALES:性転換薬 = 1
ITEMSALES:時の砂時計 = 1
ITEMSALES:永続的利尿剤 = 1
ITEMSALES:ビースティースターター = 1
ITEMSALES:排卵誘発剤 = 1
ITEMSALES:リミッター解除 = 1

DRAWLINE

REPEAT VARSIZE("ITEM")
	SIF !ITEMSALES:COUNT || ITEMNAME:COUNT == ""
		CONTINUE

	;行頭ならスペースを設ける
	SIF 表示数%4 == 0
		PRINT 　
	PRINTFORMLC [{COUNT}]%ITEMNAME:COUNT%
	表示数++
REND

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [0] 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 1
		CASEELSE
			CALL ITEMBUY_DECIDE, RESULT
			RESTART
	ENDSELECT
LOOP 1

RETURN 0

@ITEMBUY_DECIDE, ARG
DRAWLINE
;アイテムの効果を表示する
PRINTFORM %ITEMNAME:ARG%:
SELECTCASE ITEMNAME:ARG
	CASE "【汚れ無視】"
		PRINTL プレイヤーに「汚れ無視」が付く
	CASE "【ドラキュラの牙】"
		PRINTL プレイヤーに「吸血鬼」が付く
	CASE "【ドラゴンの成体】"
		PRINTL プレイヤーに「ドラゴン使い」が付く 人間より一回りほど大きい竜
ENDSELECT

;購入の意志を再確認するように
SELECTCASE ARG
	CASE 0 TO 49
		PRINTFORML 本当に%ITEMNAME:ARG%を購入しますか？
		PRINTL [0] 購入する
		PRINTL [1] やめる
		DO
			INPUT
			SIF RESULT == 1
				RETURN 0
		LOOP LOOPRES(0, 1)
		PRINTFORMW ＜%ITEMNAME:ARG%を購入しました＞
		ITEMSALES:ARG = 0
		SELECTCASE ITEMNAME:ARG
			CASE "【汚れ無視】"
				TALENT:MASTER:汚れ無視 = 1
			CASE "【ドラキュラの牙】"
				TALENT:MASTER:吸血鬼 = 1
			CASE "【ドラゴンの成体】"
				TALENT:MASTER:ドラゴン使い = 1
				DO
					PRINTL ドラゴンの名前を入力してください(何も入力しない場合、「ドラゴン」になります)
					PRINTBUTTON "[ランダムで命名する]", "ランダムで命名する"
					PRINTL 
					INPUTS
					IF RESULTS == "ランダムで命名する"
						;元ネタは分かる人だけ分かれば
						SELECTCASE RAND:28
							CASE 0
								RESULTS = ローランド
							CASE 1
								RESULTS = スタン
							CASE 2
								RESULTS = クラッシュ
							CASE 3
								RESULTS = トレント
							CASE 4
								RESULTS = テッラ
							CASE 5
								RESULTS = プレツァル
							CASE 6
								RESULTS = スクォールン
							CASE 7
								RESULTS = ヴァス
							CASE 8
								RESULTS = ケルヴィン
							CASE 9
								RESULTS = リドリー
							CASE 10
								RESULTS = ノヴァ
							CASE 11
								RESULTS = アポロ
							CASE 12
								RESULTS = スピリッツ
							CASE 13
								RESULTS = フリント
							CASE 14
								RESULTS = ヴァージル
							CASE 15
								RESULTS = クレイトン
							CASE 16
								RESULTS = クラッカー
							CASE 17
								RESULTS = エルデン
							CASE 18
								RESULTS = デューク
							CASE 19
								RESULTS = ゼルクセス
							CASE 20
								RESULTS = ブルーザー
							CASE 21
								RESULTS = ノクターン
							CASE 22
								RESULTS = ブレイズ
							CASE 23
								RESULTS = ゼラタン
							CASE 24
								RESULTS = デクスター
							CASE 25
								RESULTS = グリフ
							CASE 26
								RESULTS = レネス
							CASE 27
								RESULTS = エリオット
						ENDSELECT
					ENDIF
					SIF RESULTS == ""
						RESULTS = ドラゴン
					PRINTFORML %RESULTS%でよろしいですか？(後からオプション画面で変更できます)
					PRINTL [0] - はい
					PRINTL [1] - いいえ
					INPUT
					SELECTCASE RESULT
						CASE 0
							CSTR:MASTER:ドラゴン = %RESULTS%
							SELECTCASE CSTR:MASTER:ドラゴン
								CASE "ドラゴン"
									PRINTFORMW ドラゴンは名前が欲しいと言わんばかりに寂しげな瞳で%CALLNAME:MASTER%を見つめている……
								CASEELSE
									PRINTFORMW %CSTR:MASTER:ドラゴン%は名前をつけてくれた%CALLNAME:MASTER%に懐いているようだ……
							ENDSELECT
						CASE 1
							CONTINUE
					ENDSELECT
				LOOP LOOPRES(0)
		ENDSELECT
	CASE 50 TO 99
		;奴隷に使用するタイプのアイテムも専用の関数で
		CALL USE_ITEM, ARG
ENDSELECT

RETURN 0

;アイテム使用
@USE_ITEM, ARG
#DIM DYNAMIC 使用可
#DIMS DYNAMIC 追加情報
#DIM DYNAMIC 使用対象
#DIM DYNAMIC 表示人数

SELECTCASE ITEMNAME:ARG
	CASE "怪しげな栄養剤"
		PRINTL 体力を回復します
	CASE "ふたなり薬"
		PRINTL ふたなりになります
	CASE "ふたなり消去薬"
		PRINTL ふたなりを消去します
	CASE "天使のミルク"
		PRINTL 母乳体質になります
	CASE "ユニコーンテイル"
		PRINTL 処女膜が戻ります
	CASE "豊胸剤"
		PRINTL 胸を大きくします
	CASE "縮胸剤"
		PRINTL 胸を小さくします
	CASE "止乳剤"
		PRINTL 母乳が出なくなります
	CASE "夜尿症改善薬"
		PRINTL お漏らし癖が治ります
	CASE "ゾンビパウダー"
		PRINTL ゾンビ化します
	CASE "聖水"
		PRINTL 素質「ゾンビ」を消去します
	CASE "性転換薬"
		PRINT 対象を性転換させます。
		FONTBOLD
		PRINTL 一度性転換させると二度と戻せないので注意
		FONTREGULAR
	CASE "時の砂時計"
		PRINTL 大人になったり子供になったりします。女性ならバストサイズも変わります
	CASE "永続的利尿剤"
		PRINTL 尿を漏らしやすくなります
	CASE "ビースティースターター"
		PRINTL 獣耳と尻尾を生やします
	CASE "魔剤"
		PRINTL eraでモンエナ買えるって魔剤ンゴ！？
	CASE "排卵誘発剤"
		PRINTL 一日だけ妊娠確率を上昇させます
	CASE "リミッター解除"
		PRINTL 使用対象の能力上限を解除します
ENDSELECT

PRINTL 誰に使いますか？

FOR PAGING, 0, CHARANUM
	使用可 = 1
	;無機物・機械は除外
	SIF TALENT:PAGING:無機物 || CSTR:PAGING:種族 == "機械"
		使用可 = 0
	;使用不可のキャラも除外
	SIF CFLAG:PAGING:使用不可
		使用可 = 0
	;主人は除外 ただしふたなり、処女再生、胸関連、性転換薬、時の砂時計、ビースティースターター、排卵誘発剤は可能
	IF PAGING == MASTER
		SELECTCASE ITEMNAME:ARG
			CASE "ふたなり薬", "ユニコーンテイル", "豊胸剤", "縮胸剤", "性転換薬", "時の砂時計", "ビースティースターター", "排卵誘発剤"
			CASEELSE
				使用可 = 0
		ENDSELECT
	ENDIF
	SELECTCASE ITEMNAME:ARG
		CASE "怪しげな栄養剤"
			;体力がマックスだと使えない
			SIF BASE:PAGING:体力 >= MAXBASE:PAGING:体力
				使用可 = 0
		CASE "ふたなり薬"
			SIF TALENT:PAGING:ふたなり
				使用可 = 0
		CASE "ふたなり消去薬"
			;男で妊娠中だとヴァギナ塞げない
			SIF !TALENT:PAGING:ふたなり || (SEX(PAGING) == 1 && TALENT:PAGING:妊娠)
				使用可 = 0
		CASE "天使のミルク"
			SIF TALENT:PAGING:母乳体質 || (SEX(PAGING) == 1 && !TALENT:PAGING:ふたなり)
				使用可 = 0
		CASE "ユニコーンテイル"
			SIF TALENT:PAGING:処女 || TALENT:PAGING:再生処女 || (SEX(PAGING) == 1 && !TALENT:PAGING:ふたなり)
				使用可 = 0
		CASE "豊胸剤"
			SIF TALENT:PAGING:バストサイズ == 5 || SEX(PAGING) == 1
				使用可 = 0
		CASE "縮胸剤"
			SIF TALENT:PAGING:バストサイズ == 1 || SEX(PAGING) == 1
				使用可 = 0
		CASE "止乳剤"
			SIF !TALENT:PAGING:母乳体質 || SEX(PAGING) == 1
				使用可 = 0
		CASE "夜尿症改善薬"
			SIF !TALENT:PAGING:おもらし癖
				使用可 = 0
		CASE "ゾンビパウダー"
			SIF TALENT:PAGING:ゾンビ
				使用可 = 0
		CASE "聖水"
			SIF !TALENT:PAGING:ゾンビ
				使用可 = 0
		CASE "性転換薬"
			;妊娠時は使えない
			SIF TALENT:PAGING:妊娠 || TALENT:PAGING:性転換済
				使用可 = 0
		CASE "時の砂時計"
			SIF TALENT:PAGING:小人体型
				使用可 = 0
		CASE "永続的利尿剤"
			;既におもらし癖ついてると使えない
			SIF TALENT:PAGING:おもらし癖
				使用可 = 0
		CASE "ビースティースターター"
			;ケモミミ尻尾両方あったり、既に獣だったりファーリーだったりすると使えない
			SIF (TALENT:PAGING:動物耳 && TALENT:PAGING:尻尾) || CSTR:PAGING:種族 == "獣" || TALENT:PAGING:ファーリー
				使用可 = 0
		CASE "魔剤"
			;既にキメてる場合は使えない
			SIF CFLAG:PAGING:魔剤
				使用可 = 0
		CASE "排卵誘発剤"
			SIF CFLAG:PAGING:排卵誘発剤 || !VAGINA(PAGING)
				使用可 = 0
		CASE "リミッター解除"
			SIF TALENT:PAGING:リミットバースト || !CFLAG:PAGING:陥落
				使用可 = 0
	ENDSELECT

	追加情報 = 体力
	追加情報 += @"({BASE:PAGING:体力}/{MAXBASE:PAGING:体力}) %BARSTR(BASE:PAGING:体力, MAXBASE:PAGING:体力, 16)% "
	SIF TALENT:PAGING:処女
		追加情報 += "処女 "
	SIF TALENT:PAGING:再生処女
		追加情報 += "再生処女 "
	SIF TALENT:PAGING:童貞
		追加情報 += "童貞 "
	SIF TALENT:PAGING:恋慕
		追加情報 += "恋慕 "
	SIF TALENT:PAGING:淫乱
		追加情報 += "淫乱 "
	SIF TALENT:PAGING:服従
		追加情報 += "服従 "
	SIF TALENT:PAGING:親愛
		追加情報 += "親愛 "
	SIF TALENT:PAGING:娼婦
		追加情報 += "娼婦 "
	SIF TALENT:PAGING:隷属
		追加情報 += "隷属 "
	SIF TALENT:PAGING:崩壊
		追加情報 += "崩壊 "
	SIF TALENT:PAGING:リミットバースト
		追加情報 += "超 "
	SIF CFLAG:PAGING:状態 == 2
		追加情報 += "助手可 "
	;DISABLE
	IF !CFLAG:PAGING:口上非表示
		TRYCCALLFORM DISABLE_ITEM_%アイテム名%_K{NO:PAGING}
			SIF RESULT == 0
				使用可 = 0
		CATCH
		ENDCATCH
	ENDIF
	CALL PAGING, 使用可, @"%追加情報%", "ITEM USE", "戻る"
NEXT

SIF RESULT == -1
	RETURN 0

使用対象 = RESULT
CALL KOJO_MESSAGE_EVENT, ITEMNAME:ARG, 使用対象

SELECTCASE ITEMNAME:ARG
	;体力回復
	CASE "怪しげな栄養剤"
		PRINTFORMW ＜%CALLNAME:使用対象%の体力が回復した＞
		IF TALENT:使用対象:回復早い
			LOCAL = 400
		ELSEIF TALENT:使用対象:回復遅い
			LOCAL = 200
		ELSE
			LOCAL = 300
		ENDIF
		SIF TALENT:使用対象:小柄体型
			LOCAL += 50
		LOCAL += ABL:MASTER:技巧*10
		BASE:使用対象:体力 += LOCAL
		SIF BASE:使用対象:体力 > MAXBASE:使用対象:体力
			BASE:使用対象:体力 = MAXBASE:使用対象:体力
	;ふたなり化
	CASE "ふたなり薬"
		PRINTFORMW ＜%CALLNAME:使用対象%はふたなりになった＞
		TALENT:使用対象:ふたなり = 1
		CFLAG:使用対象:ストレス値 += 5
		SELECTCASE SEX(使用対象)
			;対象が男なら、常に処女になる Ｖ経験があれば再生処女
			CASE 1
				IF !EXP:使用対象:Ｖ経験
					TALENT:使用対象:処女 = 1
				ELSE
					TALENT:使用対象:再生処女 = 1
				ENDIF
			;対象が女で、挿入経験が無ければ童貞になる
			CASE 2
				IF VIRGIN:使用対象:0 == "" && !TALENT:使用対象:性転換済
					TALENT:使用対象:童貞 = 1
				ELSEIF VIRGIN:LOCAL0 == "" && TALENT:使用対象:性転換済
					TALENT:使用対象:童貞 = 1
				ENDIF
		ENDSELECT
	;ふたなりを消去
	CASE "ふたなり消去薬"
		PRINTFORMW ＜%CALLNAME:使用対象%はふたなりではなくなった＞
		TALENT:使用対象:ふたなり = 0
		CFLAG:使用対象:ストレス値 += 3
		SELECTCASE SEX(使用対象)
			CASE 1
				TALENT:使用対象:処女 = 0
				TALENT:使用対象:再生処女 = 0
				CALL CLEAR_FLAG, 使用対象
			CASE 2
				TALENT:使用対象:童貞 = 0
		ENDSELECT
	;母乳体質
	CASE "天使のミルク"
		PRINTFORMW ＜%CALLNAME:使用対象%は母乳が出るようになった＞
		TALENT:使用対象:母乳体質 = 1
		CFLAG:使用対象:ストレス値 += 5
	;処女膜再生
	CASE "ユニコーンテイル"
		PRINTFORMW ＜%CALLNAME:使用対象%の処女膜が再生された＞
		TALENT:使用対象:再生処女 = 1
		CFLAG:使用対象:ストレス値 += 5
	;豊乳化
	CASE "豊胸剤"
		SELECTCASE TALENT:使用対象:バストサイズ
			;絶壁→貧乳
			CASE 1
				PRINTFORMW ＜%CALLNAME:使用対象%の胸がほんの少しだけ膨らんだ＞
			;貧乳→普乳
			CASE 2
				PRINTFORMW ＜%CALLNAME:使用対象%の胸が人並みに大きくなった＞
			;普乳→巨乳
			CASE 3
				PRINTFORMW ＜%CALLNAME:使用対象%の胸が豊満になった＞
			;巨乳→爆乳
			CASE 4
				;小柄体型、小人体型
				IF TALENT:使用対象:小柄体型 || TALENT:使用対象:小人体型
					PRINTFORMW ＜%CALLNAME:使用対象%の胸が体に不釣合いなまでに大きくなった＞
				;それ以外
				ELSE
					PRINTFORMW ＜%CALLNAME:使用対象%の胸が溢れんばかりの大きさになった＞
				ENDIF
		ENDSELECT
		TALENT:使用対象:バストサイズ++
		CFLAG:使用対象:ストレス値 += 7
	;縮乳化
	CASE "縮胸剤"
		SELECTCASE TALENT:使用対象:バストサイズ
			;爆乳→巨乳
			CASE 5
				PRINTFORMW ＜%CALLNAME:使用対象%の豊かすぎる胸が一回り小さくなった＞
			;巨乳→普乳
			CASE 4
				PRINTFORMW ＜%CALLNAME:使用対象%の胸が人並みに小さくなった＞
			;普乳→貧乳
			CASE 3
				PRINTFORMW ＜%CALLNAME:使用対象%の胸が小さくなった＞
			;貧乳→絶壁
			CASE 2
				PRINTFORMW ＜%CALLNAME:使用対象%の胸が極端に薄くなった＞
				IF TALENT:使用対象:母乳体質
					PRINTFORMW ＜%CALLNAME:使用対象%の母乳が出なくなった＞
					TALENT:使用対象:母乳体質 = 0
					CFLAG:使用対象:ストレス値++
				ENDIF
		ENDSELECT
		TALENT:使用対象:バストサイズ--
		CFLAG:使用対象:ストレス値 += 7
	;母乳治療
	CASE "止乳剤"
		PRINTFORMW ＜%CALLNAME:使用対象%は母乳が出なくなった＞
		TALENT:使用対象:母乳体質 = 0
		CFLAG:使用対象:ストレス値 += 3
	;お漏らし治療
	CASE "夜尿症改善薬"
		PRINTFORMW ＜%CALLNAME:使用対象%のお漏らし癖が治った＞
		TALENT:使用対象:おもらし癖 = 0
		CFLAG:使用対象:おもらし改善フラグ = 1
		CFLAG:使用対象:ストレス値 += 3
	;ゾンビパウダー
	CASE "ゾンビパウダー"
		PRINTFORMW ＜%CALLNAME:使用対象%はゾンビになった＞
		TALENT:使用対象:ゾンビ = 1
		CFLAG:使用対象:ストレス値 += 10
	;聖水
	CASE "聖水"
		PRINTFORMW ＜%CALLNAME:使用対象%はゾンビから蘇った＞
		TALENT:使用対象:ゾンビ = 0
		IF !TALENT:使用対象:回復遅い
			PRINTW その反動で治癒力が低下しました
			TALENT:使用対象:回復遅い = 1
		ENDIF
		CFLAG:使用対象:ストレス値 += 5
	;性転換薬 ふたなり時に何があろうと無条件でバージンは復活する
	CASE "性転換薬"
		SELECTCASE SEX(使用対象)
			CASE 1
				PRINTFORMW ＜%CALLNAME:使用対象%は女になった＞
				IF TALENT:使用対象:童貞
					VIRGIN:使用対象:0 = NOTLOSTVIRGIN
					TALENT:使用対象:童貞 = 0
				ENDIF
				TALENT:使用対象:性別 = 2
				TALENT:使用対象:処女 = 1
				IF TALENT:使用対象:小柄体型
					TALENT:使用対象:バストサイズ = 2
				ELSE
					TALENT:使用対象:バストサイズ = 3
				ENDIF
			CASE 2
				PRINTFORMW ＜%CALLNAME:使用対象%は男になった＞
				IF TALENT:使用対象:処女
					VIRGIN:LOCAL = NOTLOSTVIRGIN
					TALENT:使用対象:処女 = 0
				ENDIF
				TALENT:使用対象:性別 = 1
				TALENT:使用対象:童貞 = 1
				TALENT:使用対象:バストサイズ = 0
		ENDSELECT
		CALL CLEAR_FLAG, 使用対象
		TALENT:使用対象:性転換済 = 1
		CFLAG:使用対象:ストレス値 += 20
	;時の砂時計
	CASE "時の砂時計"
		IF TALENT:使用対象:小柄体型
			PRINTFORMW ＜%CALLNAME:使用対象%は立派な大人の体になった＞
			TALENT:使用対象:小柄体型 = 0
			SIF SEX(使用対象) == 2 && TALENT:使用対象:バストサイズ != 5
				TALENT:使用対象:バストサイズ++
			MAXBASE:使用対象:体力 += 200
			BASE:使用対象:体力 += 200
		ELSE
			PRINTFORMW ＜%CALLNAME:使用対象%は\@ SEX(使用対象) == 1 ? 少年 # 幼女 \@のような小さな体になった＞
			TALENT:使用対象:小柄体型 = 1
			SIF SEX(使用対象) == 2 && TALENT:使用対象:バストサイズ != 1
				TALENT:使用対象:バストサイズ--
			MAXBASE:使用対象:体力 -= 200
			BASE:使用対象:体力 -= 200
		ENDIF
		CFLAG:使用対象:ストレス値 += 10
	;永続的利尿剤
	CASE "永続的利尿剤"
		PRINTFORMW ＜%CALLNAME:使用対象%は尿意を我慢できなくなった＞
		TALENT:使用対象:おもらし癖 = 1
		CFLAG:使用対象:ストレス値 += 10
	;ビースティースターター
	CASE "ビースティースターター"
		PRINTFORM ＜%CALLNAME:使用対象%に
		SIF !TALENT:使用対象:動物耳
			PRINT 動物の耳
		SIF !TALENT:使用対象:動物耳 && !TALENT:使用対象:尻尾
			PRINT と
		SIF !TALENT:使用対象:尻尾
			PRINT 動物の尻尾
		PRINTW が生えた＞
		TALENT:使用対象:動物耳 = 1
		TALENT:使用対象:尻尾 = 1
		IF TALENT:使用対象:好奇心
			CFLAG:使用対象:依存度 += 50
		ELSE
			CFLAG:使用対象:ストレス値 += 3
		ENDIF
	;魔剤
	CASE "魔剤"
		PRINTFORMW ＜%CALLNAME:使用対象%のテンションが上がってきた！＞
		CFLAG:使用対象:魔剤 = 1
		TIMES CFLAG:使用対象:魔剤耐性, 1.1
		CFLAG:使用対象:魔剤耐性 = LIMIT(CFLAG:使用対象:魔剤耐性, 1100, 2000)
		CFLAG:使用対象:回復量低下 += 5
		CFLAG:使用対象:ストレス値 += 10
	;排卵誘発剤
	CASE "排卵誘発剤"
		IF 使用対象 == MASTER
			PRINTFORMW ＜%CALLNAME:MASTER%は排卵誘発剤を使用した＞
		ELSE
			PRINTFORMW ＜%CALLNAME:使用対象%に排卵誘発剤を使った＞
		ENDIF
		CFLAG:使用対象:排卵誘発剤 = 1
		CFLAG:使用対象:ストレス値 += 3
	;リミットバースト
	CASE "リミッター解除"
		PRINTFORMW ＜%NAME:使用対象%のリミッターを解除した＞
		TALENT:使用対象:リミットバースト = 1
ENDSELECT

RETURN 0
