;調教対象選択(eraBEMANI)
@CHANGE_TARGET
VARSET LOCAL, 0
VARSET LOCALS, 

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

;調教可能キャラの終端を確認
REPEAT CHARANUM
	SIF COUNT != MASTER && !CHECK_CHILD_CARE(COUNT) && !CFLAG:COUNT:使用不可
		LOCAL = COUNT
REND

DRAWLINE
CALL LIFE_LIST, 1
;RESULT:0には選んだキャラ、RESULT:1には未表示キャラが居るかどうか
DRAWLINE
PRINTPLAIN 　CHANGE TARGET　
IF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
ELSE
	PRINTPLAIN                
ENDIF

PRINTFORM [1000] 戻る 

IF RESULT:1
	PRINTFORM [1001] 次ページ 
ELSE
	PRINTPLAIN                 
ENDIF

PRINT [1010] キャラソート 
PRINTL [1011] 登場作品で絞る

DO
	INPUT
	SELECTCASE RESULT
		CASE MASTER
			PRINTL 自分を調教することはできません
			RESTART
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
			RESTART
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1
				TFLAG:ページ数++
			RESTART
		CASE 1010
			CALL CHARA_SORT
			RESTART
		CASE 1011
			CALL 登場作品フィルタ設定
			TFLAG:ページ数 = 0
			BREAK
		CASE 0 TO CHARANUM-1
			SIF CHECK_CHILD_CARE(RESULT)
				CONTINUE
			SIF CFLAG:RESULT:使用不可
				CONTINUE
			SIF RESULT != 0
				TARGET = RESULT
			SIF TARGET == ASSI
				ASSI = -1
			CALL KOJO_MESSAGE_EVENT, "調教対象指定"
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

@SELECT_ASSI, ARG
#DIM DYNAMIC LTARGET
VARSET LOCAL, 0
VARSET LOCALS, 

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

;助手可能キャラの終端を確認
REPEAT CHARANUM
	SIF COUNT != MASTER && !CHECK_CHILD_CARE(COUNT) && CFLAG:COUNT:状態 >= 2
		LOCAL = COUNT
REND
DRAWLINE
CALL LIFE_LIST, 2
;RESULT:0には選んだキャラ、RESULT:1にはまだ表示されてないキャラが居るかどうか
DRAWLINE
PRINTPLAIN 　SELECT ASSI　
IF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
ELSE
	PRINTPLAIN                
ENDIF

PRINTFORM [1000] 戻る 

IF RESULT:1
	PRINTFORM [1001] 次ページ 
ELSE
	PRINTPLAIN                 
ENDIF

PRINT [1010] キャラソート 
PRINTL [1011] 登場作品で絞る

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
			RESTART
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1
				TFLAG:ページ数++
			RESTART
		CASE 1010
			CALL CHARA_SORT
			RESTART
		CASE 1011
			CALL 登場作品フィルタ設定
			TFLAG:ページ数 = 0
			BREAK
		CASE 0
			ASSI = -1
			RETURN 0
		CASE 0 TO CHARANUM-1
			SIF CFLAG:RESULT:状態 < 2
				CONTINUE
			SIF CHECK_CHILD_CARE(RESULT)
				CONTINUE
			SIF CFLAG:RESULT:使用不可
				CONTINUE
			ASSI = RESULT
			CALL KOJO_MESSAGE_EVENT, "助手指定", RESULT
			;助手にした際の素質変化用関数
			CALL ASSI_CHANGE
			ISASSI:ASSI = 1
			SIF ASSI == TARGET
				TARGET = -1
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

;LIFE_LISTは引数に1を指定すると、調教対象選択用の表示になる
;引数を2にすると、助手選択用の表示になる
;それ以外は通常表示
@LIFE_LIST, ARG
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC カウント数
#DIM DYNAMIC 未表示キャラ
#DIM DYNAMIC 対象表示
#DIM DYNAMIC 助手表示
VARSET LOCAL, 0
DRAWLINE
IF ARG == 1
	PRINTL □調教対象変更
ELSEIF ARG == 2
	PRINTL □助手変更
ELSE
	PRINTL □奴隷一覧
ENDIF
FOR LCOUNT, 0, CHARANUM
	RESETCOLOR
	;1巡目はMASTERとTARGET及びASSIの能力表示
	IF LCOUNT == 0
		IF ARG == 2
			PRINTL 　[ 0] 助手無し
		ELSE
			PRINTFORM 　[ 0] %CALLNAME:MASTER%(主人) 
			SELECTCASE SEX(MASTER)
				CASE 1
					PRINT ♂ 
				CASE 2
					PRINT ♀ 
			ENDSELECT
			SIF TALENT:MASTER:ふたなり
				PRINT 双
			PRINTL 
		ENDIF
		$表示用
		;TARGETとASSIはMASTERの下に表示する
		IF TARGET > 0 && !対象表示
			LOCAL = TARGET
			対象表示 = 1
			SIF ARG == 2 && CFLAG:状態 < 2
				GOTO 表示用
			GOTO 対象助手
		ENDIF
		IF ASSI > 0 && !助手表示
			LOCAL = ASSI
			助手表示 = 1
			GOTO 対象助手
		ENDIF
		;表示するキャラが居なければDRAWLINE後にLCOUNTを1にして通常の表示処理に戻る
		DRAWLINE
		LCOUNT = 1
		SIF CHARANUM == 1
			BREAK
	ENDIF
	LOCAL = LCOUNT
	;2巡目以降は通常のキャラ表示なので条件を指定
	IF LCOUNT > 0
		;キャラを10人表示したら終了
		IF 表示数 >= 10
			未表示キャラ = 1
			BREAK
		ENDIF
		;助手の指定なら助手不可能なキャラは除外
		SIF ARG == 2 && CFLAG:LOCAL:状態 < 2
			CONTINUE
		;登場作品フィルタが設定してあるならそれ以外のキャラは除外
		SIF 登場作品フィルタ != "" && GROUPMATCH(登場作品フィルタ, CSTR:LOCAL:登場作品, CSTR:LOCAL:登場作品２, CSTR:LOCAL:登場作品３) == 0
			CONTINUE
		;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラは灰色表示
		SIF ARG && CHECK_CHILD_CARE(LOCAL)
			SETCOLORBYNAME gray
		;調教対象の指定、もしくは助手の指定なら、使用不可のキャラは灰色表示
		SIF ARG && CFLAG:LOCAL:使用不可
			SETCOLORBYNAME gray
		;対象は主人の下に表示されるので除外
		SIF TARGET == LOCAL
			CONTINUE
		;助手も同じく
		SIF ASSI == LOCAL
			CONTINUE
		カウント数++
		;ページ数×10まで表示を飛ばす
		SIF TFLAG:ページ数*10 >= カウント数
			CONTINUE
		表示数++
	ENDIF

	$対象助手
	IF CFLAG:LOCAL:使用不可
		PRINT 不
	ELSEIF CHECK_CHILD_CARE(LOCAL)
		PRINT 妊
	ELSEIF LOCAL == TARGET
		PRINT ☆
	ELSEIF LOCAL == ASSI
		PRINT ★
	ELSEIF ISASSI:LOCAL
		PRINT ●
	ELSEIF CFLAG:LOCAL:状態 >= 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF

	;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラと使用不可キャラは選べない
	IF ARG && (CHECK_CHILD_CARE(LOCAL) || CFLAG:LOCAL:使用不可)
		PRINTFORM [--] %CALLNAME:LOCAL, 20, LEFT%
	ELSE
		PRINTFORM [{LOCAL, 2}] %CALLNAME:LOCAL, 20, LEFT%
	ENDIF

	;上記のキャラを表示したら詳細情報も表示
	PRINT 体力
	BAR BASE:LOCAL:体力, MAXBASE:LOCAL:体力, 10
	PRINTFORM ({BASE:LOCAL:体力}/{MAXBASE:LOCAL:体力}) 
	SELECTCASE SEX(LOCAL)
		CASE 1
			PRINT ♂ 
		CASE 2
			PRINT ♀ 
	ENDSELECT
	IF ARG == 1
		SIF TALENT:LOCAL:処女
			PRINT 処女 
		SIF TALENT:LOCAL:再生処女
			PRINT 再生処女 
		SIF TALENT:LOCAL:童貞
			PRINT 童貞 
		SIF TALENT:LOCAL:恋慕
			PRINT 恋慕 
		SIF TALENT:LOCAL:淫乱
			PRINT 淫乱 
		SIF TALENT:LOCAL:服従
			PRINT 服従 
		SIF TALENT:LOCAL:親愛
			PRINT 親愛 
		SIF TALENT:LOCAL:娼婦
			PRINT 娼婦 
		SIF TALENT:LOCAL:隷属
			PRINT 隷属 
		SIF TALENT:LOCAL:崩壊
			PRINT 崩壊 
		SIF TALENT:LOCAL:ふたなり
			PRINT 双 
		SIF TALENT:LOCAL:リミットバースト
			PRINT 超
	ELSEIF ARG == 2
		SELECTCASE SEX(LOCAL)
			CASE 1
				PRINTFORM ♂ 技巧Lv{ABL:LOCAL:技巧} ホモっ気Lv{ABL:LOCAL:ホモっ気} ゲイ中毒Lv{ABL:LOCAL:ゲイ中毒} 
			CASE 2
				PRINTFORM ♀ 技巧Lv{ABL:LOCAL:技巧} レズっ気Lv{ABL:LOCAL:レズっ気} レズ中毒Lv{ABL:LOCAL:レズ中毒} 
		ENDSELECT
		SIF TALENT:LOCAL:両刀
			PRINT 両刀 
		SIF TALENT:LOCAL:ふたなり
			PRINT ふたなり
	ENDIF
	PRINTL 
	RESETCOLOR
	;対象及び助手が表示できるなら表示
	SIF (TARGET > 0 && !対象表示) || (ASSI > 0 && !助手表示)
		GOTO 表示用
	;一巡目ならDRAWLINEしてCONTINUE
	IF LCOUNT == 0
		DRAWLINE
		CONTINUE
	ENDIF
NEXT
DRAWLINE
PRINTL 　☆：調教中　★：助手　○：助手可　●：元助手　不：使用不可　妊：妊娠育児中　を表しています
SIF 登場作品フィルタ != ""
	PRINTFORML 　現在、「%登場作品フィルタ%」に登場するキャラのみ表示しています

;条件を満たしているにも関わらずまだリスト表示されてないキャラが居れば未表示キャラフラグは1になる
RETURN RESULT, 未表示キャラ

;ステータス表示
@SHOW_CHARADATA
DRAWLINE
LOCALS =

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

CALL LIFE_LIST
;RESULT:0には選んだキャラ、RESULT:1にはまだ表示されてないキャラが居るかどうか
DRAWLINE

PRINTPLAIN 　CHARADATA　
IF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
ELSE
	PRINTPLAIN                
ENDIF

PRINTFORM [1000] 戻る 
IF RESULT:1
	PRINTFORM [1001] 次ページ 
ELSE
	PRINTPLAIN                 
ENDIF

PRINT [1010] キャラソート 
PRINTL [1011] 登場作品で絞る

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1
				TFLAG:ページ数++
		CASE 1010
			CALL CHARA_SORT
			BREAK
		CASE 1011
			CALL 登場作品フィルタ設定
			TFLAG:ページ数 = 0
			BREAK
		CASE 0 TO CHARANUM-1
			CALL STATUS, RESULT
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

RESTART

@CHARA_SORT
VARSET LOCAL, 0
DRAWLINE
PRINTL □どの要素でソートしますか？
PRINTL 　[1] - 刻印
PRINTL 　[2] - 能力
PRINTL 　[3] - 経験
PRINTL 　[4] - 名前
PRINTL 　[5] - キャラ番号
PRINTL 　[6] - 体力
PRINTL 　[7] - 気力
DRAWLINE
PRINTPLAIN 　CHARA SORT　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			DRAWLINE
			PRINTL □刻印
			REPEAT 4
				SIF MARKNAME:COUNT != ""
					PRINTFORML 　[{COUNT}] - %MARKNAME:COUNT%
			REND
			DRAWLINE
			PRINTPLAIN 　MARK　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 4
						SIF MARKNAME:RESULT == ""
							CONTINUE
						PRINTL □並べ替え
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　MARK　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %MARKNAME:LOCAL%で昇順にソートしました
									SORTCHARA MARK:LOCAL
								CASE 1
									PRINTFORMW %MARKNAME:LOCAL%で降順にソートしました
									SORTCHARA MARK:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 2
			DRAWLINE
			LOCAL = 0
			PRINTL □能力
			REPEAT 50
				SIF ABLNAME:COUNT == ""
					CONTINUE
				IF LOCAL == 0 || LOCAL % 4 == 0
					PRINTFORMLC 　[{COUNT, 2}] - %ABLNAME:COUNT%
				ELSE
					PRINTFORMLC [{COUNT, 2}] - %ABLNAME:COUNT%
				ENDIF
				LOCAL++
			REND
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　ABL　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 50
						PRINTL □並べ替え
						SIF ABLNAME:RESULT == ""
							CONTINUE
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %ABLNAME:LOCAL%で昇順にソートしました
									SORTCHARA ABL:LOCAL
								CASE 1
									PRINTFORMW %ABLNAME:LOCAL%で降順にソートしました
									SORTCHARA ABL:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 3
			DRAWLINE
			LOCAL = 0
			PRINTL □経験
			REPEAT 100
				SIF EXPNAME:COUNT == ""
					CONTINUE
				IF LOCAL == 0 || LOCAL % 4 == 0
					PRINTFORMLC 　[{COUNT, 2}] - %EXPNAME:COUNT%
				ELSE
					PRINTFORMLC [{COUNT, 2}] - %EXPNAME:COUNT%
				ENDIF
				LOCAL++
			REND
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　EXP　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 99
						PRINTL □並べ替え
						SIF EXPNAME:LOCAL == ""
							CONTINUE
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %EXPNAME:LOCAL%で昇順にソートしました
									SORTCHARA EXP:LOCAL
								CASE 1
									PRINTFORMW %EXPNAME:LOCAL%で降順にソートしました
									SORTCHARA EXP:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 4
			DRAWLINE
			PRINTL □名前
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　NAME　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 名前で昇順にソートしました
						SORTCHARA NAME
					CASE 1
						PRINTW 名前で降順にソートしました
						SORTCHARA NAME, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 5
			DRAWLINE
			PRINTL □番号
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　NUMBER　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW キャラ番号で昇順にソートしました
						SORTCHARA
					CASE 1
						PRINTW キャラ番号で降順にソートしました
						SORTCHARA BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 6
			DRAWLINE
			PRINTL □体力
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　HEALTH　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 体力で昇順にソートしました
						SORTCHARA BASE:体力
					CASE 1
						PRINTW 体力で降順にソートしました
						SORTCHARA BASE:体力, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 7
			DRAWLINE
			PRINTL □気力
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　VITAL　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 気力で昇順にソートしました
						SORTCHARA BASE:気力
					CASE 1
						PRINTW 気力で降順にソートしました
						SORTCHARA BASE:気力, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 4, 5, 6, 7)

RETURN 0

@登場作品フィルタ設定
#DIMS DYNAMIC 原作, 300
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 登場作品
#DIM DYNAMIC 登場作品２
#DIM DYNAMIC 登場作品３
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;動的変数だけど一応初期化しておく
VARSET 原作

;GETNUMの記述を省略
登場作品 = GETNUM(CSTR, "登場作品")
登場作品２ = GETNUM(CSTR, "登場作品２")
登場作品３ = GETNUM(CSTR, "登場作品３")

;過去のセーブデータのためにCSTRを代入する
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:登場作品 != CSVCSTR(NO:LCOUNT, 登場作品)
		CSTR:LCOUNT:登場作品 = %CSVCSTR:(NO:LCOUNT, 登場作品)%
	SIF CSTR:LCOUNT:登場作品２ != CSVCSTR(NO:LCOUNT, 登場作品２)
		CSTR:LCOUNT:登場作品２ = %CSVCSTR:(NO:LCOUNT, 登場作品２)%
	SIF CSTR:LCOUNT:登場作品３ != CSVCSTR(NO:LCOUNT, 登場作品３)
		CSTR:LCOUNT:登場作品３ = %CSVCSTR:(NO:LCOUNT, 登場作品３)%
NEXT

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;変数「原作」の配列に該当作品がまだ入ってない場合は入れる
	IF CSVCSTR(LCOUNT, 登場作品) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品))) == -1
			原作:FINDELEMENT(原作, "", 0, 99, 1) = %CSVCSTR(LCOUNT, 登場作品)%
	ENDIF
	;登場作品２は原作:100〜199の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品２) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品２))) == -1
			原作:FINDELEMENT(原作, "", 100, 199, 1) = %CSVCSTR(LCOUNT, 登場作品２)%
	ENDIF
	;登場作品３は原作:200〜299の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品３) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品３))) == -1
			原作:FINDELEMENT(原作, "", 200, 299, 1) = %CSVCSTR(LCOUNT, 登場作品３)%
	ENDIF
NEXT

DO
	;配列の使っていない範囲を検索してARRAYMOVEで前詰めする
	始点 = FINDELEMENT(原作, "", 0, VARSIZE("原作"), 1)
	FOR LCOUNT, 始点, VARSIZE("原作")
		IF 原作:LCOUNT != ""
			終点 = LCOUNT
			BREAK
		ENDIF
		;配列の終端にたどり着いたら前詰め処理終了
		SIF LCOUNT == VARSIZE("原作")-1
			GOTO BREAK
	NEXT
	ARRAYREMOVE 原作, 始点, 終点-始点
LOOP 1
$BREAK

PRINTL □表示したいキャラの登場作品を選んでください
FOR LCOUNT, 0, VARSIZE("原作")
	SIF 原作:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %原作:LCOUNT%
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTPLAIN 　原作別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("原作")
			IF 原作:RESULT != ""
				登場作品フィルタ = %原作:RESULT%
				RETURN
			ENDIF
		CASE -1
			RETURN
		CASE -2
			登場作品フィルタ = 
			RETURN
	ENDSELECT
LOOP 1

