@EVENTFIRST
TARGET = -1
ASSI = -1

VARLINE = 0

;コンフィグ初期設定
FLAG:地の文表示 = 1
FLAG:口上表示 = 1

LOADGLOBAL

;モード選択 eraOriではeraBEMANIでいうPFREE相当のモードだけ
CALL SETUP_GAMESTART
ADDCHARA 0
MASTER = 0
CALL START_CHARA_SELECT

DRAWLINE
IF デフォルトオプション
	FLAG:地の文表示 = 地の文設定
	FLAG:口上表示 = 口上設定
ELSE
	PRINTL 口上を表示しますか？(オプションでも変更できます)
	PRINTL [0] - 口上+地の文を表示する
	PRINTL [1] - 口上のみ表示する
	PRINTL [2] - 地の文のみ表示する
	PRINTL [3] - 口上も地の文も表示しない
	DO
		INPUT
		SELECTCASE RESULT
			CASE 0
				FLAG:地の文表示 = 1
				FLAG:口上表示 = 1
			CASE 1
				FLAG:地の文表示 = 0
				FLAG:口上表示 = 1
			CASE 2
				FLAG:地の文表示 = 1
				FLAG:口上表示 = 0
			CASE 3
				FLAG:地の文表示 = 0
				FLAG:口上表示 = 0
		ENDSELECT
	LOOP LOOPRES(0, 1, 2, 3)
ENDIF

IF デフォルトオプション
	SIF 着衣オプション設定
		FLAG:着衣設定 = 3
ELSE
	DRAWLINE
	PRINTL 奴隷の着衣設定を変更しますか？（オプションでも変更できます）
	PRINTL [0] - 服を着たままでいい
	PRINTL [1] - 面倒だから脱がせてから調教始める
	DO
		INPUT
		SELECTCASE RESULT
			CASE 1
				FLAG:着衣設定 = 3
		ENDSELECT
	LOOP LOOPRES(0, 1)
ENDIF

BEGIN SHOP

@START_CHARA_SELECT
LOADGLOBAL
DRAWLINE
PRINTL ★★プレイヤーを変更できます★★
PRINTL ★★キャラを選択してください★★

CALL CHARA_LIST, 2

SELECTCASE RESULT
	CASE 0
		PRINTW プレイヤーを変更しませんでした
		CALL SELECT_SEX
		CALL INPUT_NAME
		CALL SELECT_STYLE
		SELECTCASE FLAG:ゲームモード
			CASE 9
				CALL SELECT_VIRGIN
		ENDSELECT
	CASEELSE
		DELCHARA 0
		ADDCHARA RESULT
		MASTER = 0
		PRINTFORMW プレイヤーを「%NAME:MASTER%」に設定します
		DRAWLINE
		IF NO:MASTER != 0
			PRINTFORML %CALLNAME:MASTER%に調教されたい人向けに奴隷としてあなたの素質を変更しますか？
			PRINTL [0] - 調教されたい！
			PRINTL [1] - いらん
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						ADDCHARA 0
						TARGET = 1
						CALL SELECT_SEX, TARGET
						CALL INPUT_NAME, TARGET
						CALL SELECT_VIRGIN, TARGET
						CALL SELECT_STYLE, TARGET
						BREAK
					CASE 1
						BREAK
				ENDSELECT
			LOOP 1
		ENDIF
ENDSELECT

RETURN 0

@SELECT_SEX, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 性別を選択してください
PRINTL [0] - 男性
PRINTL [1] - 女性
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			TALENT:ARG:性別 = 1
			CSTR:ARG:上半身上着 = シャツ
			CSTR:ARG:下半身上着 = ズボン
			CSTR:ARG:下半身下着 = トランクス
		CASE 1
			TALENT:ARG:性別 = 2
			TALENT:ARG:バストサイズ = 3
			CSTR:ARG:全身上着 = ワンピース
			CSTR:ARG:上半身下着 = ブラジャー
			CSTR:ARG:下半身下着 = パンティ
			CFLAG:ARG:挿入可 = 1
	ENDSELECT
LOOP LOOPRES(0, 1)

@INPUT_NAME, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
DO
	PRINT 名前を入力してください(何も入力しない場合は「
	PRINTBUTTON "あなた", "あなた"
	PRINTL 」になります)
	INPUTS
	SELECTCASE RESULTS
		CASE ""
			CALLNAME:ARG = あなた
			NAME:ARG = あなた
		CASEELSE
			CALLNAME:ARG = %RESULTS%
			NAME:ARG = %RESULTS%
	ENDSELECT
	PRINTFORML 「%CALLNAME:ARG%」でよろしいですか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	INPUT
LOOP LOOPRES(0)

@SELECT_VIRGIN, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 性交の経験は……
PRINTL [0] - ある
PRINTL [1] - 無い
DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			SELECTCASE SEX(ARG)
				CASE 1
					TALENT:ARG:童貞 = 1
				CASE 2
					TALENT:ARG:処女 = 1
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(0, 1)

@SELECT_STYLE, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 体型はどんな感じですか？
PRINTL [0] - 普通の人です
SELECTCASE SEX(ARG)
	CASE 1
		PRINTL [1] - ショタです
	CASE 2
		PRINTL [1] - ロリです
ENDSELECT
DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			TALENT:ARG:小柄体型 = 1
			MAXBASE:ARG:体力 -= 200
			BASE:ARG:体力 -= 200
			SELECTCASE SEX(ARG)
				CASE 2
					TALENT:ARG:バストサイズ = 2
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(0, 1)

@SETUP_GAMESTART
DAY = 1
FLAG:色仕掛け = 1
;リアルタイム計算処理 変数REALTIMEも残しておくこと
;リアルタイム(文字列式)からリアル西暦を抜き出して数値型に変換して代入する……
REALTIME:0 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
REALTIME:1 = TOINT(SUBSTRING(GETTIMES(), 5, 2))
REALTIME:2 = TOINT(SUBSTRING(GETTIMES(), 8, 2))

YEAR = REALTIME:0
MONTH = REALTIME:1
DAYS = REALTIME:2

;曜日計算
FLAG:曜日 = ZELLER(YEAR, MONTH, DAYS)

;月齢計算
LOCAL = ((YEAR-11)%19)*11
SELECTCASE MONTH
	CASE 1, 3
		LOCAL:1 = 0
	CASE 2, 4, 5
		LOCAL:1 = 2
	CASE 6 TO 12
		LOCAL:1 = MONTH-2
ENDSELECT

月齢 = (LOCAL+LOCAL:1+DAYS)%30

VARSET LOCAL, 0

VARSET SELECTCOM, -1
SELECTCOM = 0

;気温の算出
CALL TEMPERATURE
;天気の決定
CALL WEATHER


